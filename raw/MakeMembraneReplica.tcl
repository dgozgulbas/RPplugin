##==============================================================================
# This plugin makes replicas of any membrane by randomly swapping lipids.
# It needs a NAMD conf file that can properly run on the current directory,
# or it can use a default configuration file which uses CHAMM36m parameters.
# The plugin centers the membrane in the cell before starting with the protocol.
# Moreover, it tries to detect and correct for ring piercing. It will take longer
# time to run in case of ring piercing.
# It outputs a new pdb file containing the replica.
# *** It works like a charmM-GUI ;-) ***
# 
# Authors: Giuseppe Licari, Sepehr Dehghani-Ghahnaviyeh, v1.0, 01-2020
#
## Citation: 
##   
##==============================================================================

package provide membranemixerpackage 1.0

# package requirements
package require tktooltip

#======================================================
namespace eval ::MembraneMixerPackage:: {
    variable dirPackage $env(MMPDIR)
    variable dirCharmmPar $env(CHARMMPARDIR)

    variable w 
    
    variable psffile "path2structure.psf"
    variable pdbfile "path2structure.pdb"
    variable custom_conffile 0
    variable addtopfile 0
    variable conffile "path2configuration.inp"; # this configuration file must already run properly in the current directory
    #variable conffile {}; # this configuration file must already run properly in the current directory
    variable outputpath [pwd]; #output directory, default is current directory
    # Specify atomselect containing lipid residues that one wants to exchange
    variable lselection "segname MEMB"
    # Add colvar to restrain phosphorus atoms plane in each leaflet. If cholesterol is present, then also its oxygen plane is restrain.
    # This is useful when not using a membrane generated by the CHARMM-GUI, which contains already these restraints. 
    # You can also specify the force constant for the restraints. Any other "colvar" in the file is removed if "restrPhos" is activated.
    # If the force constant for P-P distance is too low you might observe separation of the two leaflets.
    variable restrainedbilayer 1
    variable forceconstbilayer 50
    # You can also specify some groupd of atoms to be restrain (and relative force constant). You may want to restrain the protein for example,
    # but if the force constant is too high it might crash the simulation. Any other "constraint" in the file is removed if "restrGroup" is activated.
    variable restrained 0
    variable restrainedselection  "protein and noh"
    variable forceconst 30
    # Set if want to exchange full center of mass (0) and only XY, keeping Z (1). Suggested setting is 1
    variable ExProc 1
    variable exchange 50; 	# Exchanging almost all lipids may result in bad membranes
    # Number of replicas to generate
    variable nreplicas 1
    # Specify equilibration 1 and 2 protocol. Modify .conf file so to obtain restart files at the end of equilibrations
    # Variables determine number of steps for minimization and MD for each equilibration. In the second equilibration
    # 5 cycles of MD with decreasing grid force are used and "runsteps2" defines the steps in each cycle (double in the last).
    variable num_min1 1000
    variable num_md1 2000
    variable num_min2 1000
    variable num_md2 2000
    variable namdcommand "namd2"
    variable namdcommandOpt 
    # Other variables
    variable consoleMaxHistory 100
    variable consoleMessageCount 0
    variable progressFile
    variable BuildScript
    variable ChangeFreqOut 0
    variable parfiles {}
}

proc mmp {} {
    return [eval ::MembraneMixerPackage::membranemixerpackage]
}

proc membranemixerpackage_tk {} {
    MembraneMixerPackage::membranemixerpackage
    return $MembraneMixerPackage::w
}

#======================================================

### Procedure GUI: create the window and initialize data structures ###

proc ::MembraneMixerPackage::membranemixerpackage {} {

    # ::MembraneMixerPackage::init_default_topology
    variable w
    if { [winfo exists .membranemixerpackage] } {
    wm deiconify .membranemixerpackage
    return
    }
    
    set w [toplevel ".membranemixerpackage"]
    wm title $w "Membrane Mixer"
    wm resizable $w 1 1
    
    trace add variable ::MembraneMixerPackage::restrained write ::MembraneMixerPackage::write_state
    trace add variable ::MembraneMixerPackage::custom_conffile write ::MembraneMixerPackage::write_state_1
    trace add variable ::MembraneMixerPackage::restrainedbilayer write ::MembraneMixerPackage::write_state_2

    trace add variable ::MembraneMixerPackage::psffile write ::MembraneMixerPackage::psffileCheck
    trace add variable ::MembraneMixerPackage::pdbfile write ::MembraneMixerPackage::pdbfileCheck
    trace add variable ::MembraneMixerPackage::conffile write ::MembraneMixerPackage::conffileCheck
    trace add variable ::MembraneMixerPackage::outputpath write ::MembraneMixerPackage::outputpathCheck
    trace add variable ::MembraneMixerPackage::lselection write ::MembraneMixerPackage::lselectionCheck
    trace add variable ::MembraneMixerPackage::forceconstbilayer write ::MembraneMixerPackage::forceconstbilayerCheck
    trace add variable ::MembraneMixerPackage::forceconst write ::MembraneMixerPackage::forceconstCheck
    trace add variable ::MembraneMixerPackage::restrainedselection write ::MembraneMixerPackage::restrainedselectionCheck
    trace add variable ::MembraneMixerPackage::exchange write ::MembraneMixerPackage::exchangeCheck
    trace add variable ::MembraneMixerPackage::nreplicas write ::MembraneMixerPackage::nreplicasCheck
    trace add variable ::MembraneMixerPackage::num_min1 write ::MembraneMixerPackage::min1Check
    trace add variable ::MembraneMixerPackage::num_min2 write ::MembraneMixerPackage::min2Check
    trace add variable ::MembraneMixerPackage::num_md1 write ::MembraneMixerPackage::md1Check
    trace add variable ::MembraneMixerPackage::num_md2 write ::MembraneMixerPackage::md2Check
    trace add variable ::MembraneMixerPackage::namdcommand write ::MembraneMixerPackage::namdcommandCheck
    
    set f $w
    
    # Add a menu bar
    frame $f.menubar -relief raised -bd 2
    pack $f.menubar -padx 1 -fill x
    
    menubutton $f.menubar.help -text Help -underline 0 -menu $f.menubar.help.menu
    # XXX - set menubutton width to avoid truncation in OS X
    $f.menubar.help config -width 5
    
    # Help menu
    menu $f.menubar.help.menu -tearoff no
    $f.menubar.help.menu add command -label "About" \
    -command {tk_messageBox -type ok -title "About Membrane Mixer" \
    -message "This plugin generates membrane replicas with different lipid placements by randomly swapping lipids and equilibrating the membrane using short molecular dynamics simulations with grid potentials. Always check the resulting membranes!"}
    $f.menubar.help.menu add command -label "Help..." \
    -command "vmd_open_url [string trimright [vmdinfo www] /]/plugins/membranemixer"
    
    pack $f.menubar.help -side right
    
    # Input 
    frame $f.input -bd 2 -relief ridge
    
    listbox $f.input.list -activestyle dotbox -yscroll "$f.input.scroll set" -width 60 -height 2  -setgrid 1 -selectmode browse -selectbackground white \
    -listvariable ::MembraneMixerPackage::parfiles -relief sunken -exportselection 0 \
    -selectbackground lightsteelblue 
    scrollbar $f.input.scroll -command "$f.input.list yview"

    button $f.input.add -text "Add" -command [namespace code {
        set toptypes {
        {{CHARMM Topology Files} {.top .inp .rtf .prm .str}}
        {{All Files} {*}}
        }
        set temploc [tk_getOpenFile -title "Select additional parameter file(s)" -filetypes $toptypes ]
            if {$temploc!=""} {lappend ::MembraneMixerPackage::parfiles $temploc}
    }]
    button $f.input.delete -text "Delete" -command [namespace code {
        foreach i [.membranemixerpackage.input.list curselection] {
            .membranemixerpackage.input.list delete $i
        }
    }]

    grid [label $f.input.label -text "Input:"] -row 0 -column 0 -columnspan 1 -sticky w
    grid [label $f.input.psflabel -text "PSF file: "] -row 1 -column 0 -sticky w
    grid [entry $f.input.psfpath -width 30 -textvariable ::MembraneMixerPackage::psffile] -row 1 -column 1 -sticky ew
    grid [button $f.input.psfbutton -text "Browse" \
        -command {
            set tempfile [tk_getOpenFile -title "Select a PSF file"]
            if {![string equal $tempfile ""]} { set ::MembraneMixerPackage::psffile $tempfile }
        }] -row 1 -column 3 -sticky w
    foreach l {"label" "path" "button"} {::TKTOOLTIP::balloon $f.input.psf${l} "Select the .psf file containing the topology information of your initial membrane"}
    grid [label $f.input.pdblabel -text "PDB file: "] -row 2 -column 0 -sticky w
    grid [entry $f.input.pdbpath -width 30 -textvariable ::MembraneMixerPackage::pdbfile] -row 2 -column 1 -sticky ew
    grid [button $f.input.pdbbutton -text "Browse" \
        -command {
            set tempfile [tk_getOpenFile -title "Select a PDB file"]
            if {![string equal $tempfile ""]} { set ::MembraneMixerPackage::pdbfile $tempfile }
        }] -row 2 -column 3 -sticky w
    foreach l {"label" "path" "button"} {::TKTOOLTIP::balloon $f.input.pdb${l} "Select the .pdb file containing the coordinate information of your initial membrane"}
    
    grid [checkbutton $f.input.dconff -text "Use a custom configuration file" -variable ::MembraneMixerPackage::custom_conffile] -row 3 -column 0 -columnspan 2 -sticky w
    ::TKTOOLTIP::balloon $f.input.dconff "A default configuration file and CHARMM36m parameters will be used."
    
    grid [label $f.input.conflabel -text "CONF file: " -state disabled] -row 4 -column 0 -sticky w
    grid [entry $f.input.confpath -width 30 -textvariable ::MembraneMixerPackage::conffile -state disabled] -row 4 -column 1 -sticky ew
    grid [button $f.input.confbutton -text "Browse" \
        -command {
            set tempfile [tk_getOpenFile -title "Select a custom configuration file"]
            if {![string equal $tempfile ""]} { set ::MembraneMixerPackage::conffile $tempfile }
        } -state disabled] -row 4 -column 3 -sticky w
    foreach l {"label" "path" "button"} {::TKTOOLTIP::balloon $f.input.conf${l} "Provide a configuration file to run the equilibration of the membrane.\nCheck if it is already usable with the selected system without errors.\nThe file will be modified by the plugin to account for other settings."}
    
    grid [label $f.input.toplabel -text "Additional parameter files: "] -row 5 -column 0 -sticky w
    grid $f.input.list -column 0 -row 6 -columnspan 2 -rowspan 2 -sticky nswe 
    grid $f.input.scroll -column 2 -row 6 -rowspan 2 -sticky nsw 
    grid $f.input.add -column 3 -row 6 -sticky we
    grid $f.input.delete -column 3 -row 7 -sticky we
    foreach l {"list" "scroll" "add"} {::TKTOOLTIP::balloon $f.input.${l} "If needed, include additional parameter files to run NAMD simulations.\nFor instance, you can use the default configuration file and\ninclude additional parameters for residues that are not in CHARMM."}
    ::TKTOOLTIP::balloon $f.input.delete "Remove selected parameter file."

    grid [label $f.input.outputlabel -text "Output path: "] -row 8 -column 0 -sticky w
    grid [entry $f.input.outputpath -width 30 -textvariable ::MembraneMixerPackage::outputpath] -row 8 -column 1 -sticky ew
    grid [button $f.input.outputbutton -text "Browse" \
        -command {
            set tempdir [tk_chooseDirectory -title "Select an output folder"]
            if {![string equal $tempdir ""]} { set ::MembraneMixerPackage::outputpath $tempdir }
        }] -row 8 -column 3 -sticky ew
    foreach l {"label" "path" "button"} {::TKTOOLTIP::balloon $f.input.output${l} "Provide an output path in which to run the simulations.\nEach membrane replica will be moved in a separate folder."}

    grid columnconfigure $f.input 1 -weight 1
    pack $f.input -side top -padx 2 -pady 6 -expand 0 -fill both
    
    # Lipid Selection
    frame $f.selection -bd 2 -relief ridge 
    
    grid [label $f.selection.lipidselectionlabel -text "Lipid selection: "] -row 0 -column 0 -sticky w
    grid [entry $f.selection.lselection -width 30 -textvariable ::MembraneMixerPackage::lselection] -row 0 -column 1 -columnspan 2 -sticky ew
    grid [button $f.selection.lselectionGuess -text " Guess " \
        -command {
            set ::MembraneMixerPackage::lselection [::MembraneMixerPackage::GuessLipidSel]
        }] -row 0 -column 3 -sticky ew
    foreach l {"lipidselectionlabel" "lselection"} {::TKTOOLTIP::balloon $f.selection.${l} "Specify the selection that contains the lipids to be exchanged.\nThe \"resid\" is used to detect each lipid molecule."}
    ::TKTOOLTIP::balloon $f.selection.lselectionGuess "Guess \"segname\" containing lipids in input PSF and PDB files."
    grid [checkbutton $f.selection.restrainedbilayer -text "Restrain bilayer" -variable ::MembraneMixerPackage::restrainedbilayer] -row 1 -column 0 -columnspan 2 -sticky w
    grid [label $f.selection.forceconstbilayerlabel -text "Force constant \[kcal/mol\]:" -state normal] -row 1 -column 2 -sticky e
    grid [entry $f.selection.forceconstbilayer -width 8 -textvariable ::MembraneMixerPackage::forceconstbilayer -state normal] -row 1 -column 3 -sticky ew
    foreach l {"restrainedbilayer" "forceconstbilayerlabel" "forceconstbilayer"} {::TKTOOLTIP::balloon $f.selection.${l} "Add colvar to restrain phosphorus atoms (and oxygen of cholesterol, if present) in each leaflet.\nThis is highly reccomended as otherwise the two leaflets will fly apart.\nIncrease the force constant is you still observe too much leaflet separation.\nIf this option is used, any other \"colvar\" in the original conf file will be removed.\nDo not use if proper membrane restraints are already specified in the configuration file."}
    
    grid [checkbutton $f.selection.restrained -text "Restrain group" -variable ::MembraneMixerPackage::restrained] -row 2 -column 0 -columnspan 2 -sticky w
    grid [label $f.selection.forceconstlabel -text "Force constant \[kcal/mol\]:" -state disabled] -row 2 -column 2 -sticky e
    grid [entry $f.selection.forceconst -width 8 -textvariable ::MembraneMixerPackage::forceconst -state disabled] -row 2 -column 3 -sticky ew
    foreach l {"restrained" "forceconstlabel" "forceconst"} {::TKTOOLTIP::balloon $f.selection.${l} "Add a restrain on a group of atoms, e.g. on a protein.\nThis avoids the restrained group to move away from its original equilibrium conformation.\nIf this option is used, any other \"constrain\" in the original conf file will be removed.\nDo not use if proper constraints are already specified in the configuration file."}
    grid [label $f.selection.restrainedselectionlabel -text "Group selection: " -state disabled] -row 3 -column 0 -sticky w
    grid [entry $f.selection.restrainedselection -width 30 -textvariable ::MembraneMixerPackage::restrainedselection -state disabled] -row 3 -column 1 -columnspan 3 -sticky ew
    foreach l {"restrainedselectionlabel" "restrainedselection"} {::TKTOOLTIP::balloon $f.selection.${l} "Selection of group of atoms to restrain."}
    grid [checkbutton $f.selection.exProc -text "Exchange only XY, keeping original Z" -variable ::MembraneMixerPackage::ExProc] -row 4 -column 0 -columnspan 2 -sticky w
    ::TKTOOLTIP::balloon $f.selection.exProc "If this option is selected, only the X and Y coordinates of the center of mass of the two exchanging lipidi\nwill be used (best choice in most cases). Otherwise, full center of mass is used."
    
    grid columnconfigure $f.selection 1 -weight 1
    pack $f.selection -side top -padx 2 -pady 6 -expand 0 -fill both
    
    # Membrane-related settings
    frame $f.parameters -bd 2 -relief ridge
    
    grid [label $f.parameters.exchangelabel -text "Lipid exchange (%): "] -row 0 -column 0 -sticky e
    grid [entry $f.parameters.exchange -width 8 -textvariable ::MembraneMixerPackage::exchange] -row 0 -column 1 -sticky w
    foreach l {"exchangelabel" "exchange"} {::TKTOOLTIP::balloon $f.parameters.${l} "Percentage of molecules in \"Lipid Selection\" to exchange."}
    grid [label $f.parameters.nreplicaslabel -text "Num. of membrane replicas: "] -row 0 -column 2 -sticky e
    grid [entry $f.parameters.nreplicas -width 8 -textvariable ::MembraneMixerPackage::nreplicas] -row 0 -column 3 -sticky w
    foreach l {"nreplicaslabel" "nreplicas"} {::TKTOOLTIP::balloon $f.parameters.${l} "Number of membrane replicas that will be serially generated by the plugin."}
    
    grid columnconfigure $f.parameters 1 -weight 1
    pack $f.parameters -side top -padx 2 -pady 6 -expand 0 -fill both
    
    frame $f.moldyn -bd 2 -relief ridge
    
    grid [label $f.moldyn.firstmdlabel -text "First equilibration:"] -row 0 -column 0 -sticky w
    grid [label $f.moldyn.secondmdlabel -text "Second equilibration:"] -row 0 -column 2 -sticky w
    grid [label $f.moldyn.min1 -text "Min. steps: "] -row 1 -column 0 -sticky w
    grid [label $f.moldyn.min2 -text "Min. steps: "] -row 1 -column 2 -sticky e
    grid [label $f.moldyn.md1 -text "MD  steps: "] -row 2 -column 0 -sticky w
    grid [label $f.moldyn.md2 -text "MD  steps: "] -row 2 -column 2 -sticky e
    grid [entry $f.moldyn.num_min1 -width 8 -textvariable ::MembraneMixerPackage::num_min1] -row 1 -column 1 -sticky w
    grid [entry $f.moldyn.num_md1 -width 8 -textvariable ::MembraneMixerPackage::num_md1] -row 2 -column 1 -sticky w
    grid [entry $f.moldyn.num_min2 -width 8 -textvariable ::MembraneMixerPackage::num_min2] -row 1 -column 3 -sticky w
    grid [entry $f.moldyn.num_md2 -width 8 -textvariable ::MembraneMixerPackage::num_md2] -row 2 -column 3 -sticky w
    foreach l {"firstmdlabel" "min1" "md1" "num_min1" "num_md1"} {::TKTOOLTIP::balloon $f.moldyn.${l} "Before exchanging the lipids the following protocol is performed:\n 1) Generation of a lipid-repulsive grid-force potential around the lipids to exchange\n 2) Minimization of the system of \"Min. steps\" steps under the grid-force potential\n 3) MD equilibration of the system of \"MD. steps\" steps under the grid-force potential"}
    foreach l {"secondmdlabel" "min2" "md2" "num_min2" "num_md2"} {::TKTOOLTIP::balloon $f.moldyn.${l} "After exchanging the lipids the following protocol is performed:\n 1) Generation of a lipid-repulsive grid-force potential around the exchange lipids\n 2) Minimization of the system of \"Min. steps\" steps under the grid-force potential\n 3) 4 MD equilibrations of the system each of \"MD. steps\" steps under a decreasing grid-force potential\n 4) MD equilibration of the system of 2*\"MD. steps\" steps without grid-force potential"}
    
    
    grid columnconfigure $f.moldyn 1 -weight 1
    pack $f.moldyn -side top -padx 2 -pady 6 -expand 0 -fill both
    
    # Run plugin
    frame $f.run -bd 2 -relief ridge
    
    grid [label $f.run.label -text "Run:"] -row 0 -column 0 -columnspan 1 -sticky w
    
    #variable namdcommand "/Projects/namd2/bin/2.13/Linux64-multicore/namd2"
    grid [label  $f.run.namdlabel -text "NAMD path:"] -row 1 -column 0 -sticky w
    grid [entry  $f.run.namdcommand -width 30 -textvariable ::MembraneMixerPackage::namdcommand] -row 1 -column 1 -sticky ew
    #grid [button $f.run.namdbutton -text "Browse" \
    #    -command {
    #        set tempfile [tk_getOpenFile -title "Select NAMD executable"]
    #        if {![string equal $tempfile ""]} { set ::MembraneMixerPackage::namdcommand $tempfile }
    #    }] -row 1 -column 2 -sticky w
    foreach l {"label" "command" } {::TKTOOLTIP::balloon $f.run.namd${l} "Specify the path to NAMD executable."}
    #variable namdcommandOpt "-gpu +p[::MembraneMixerPackage::getProcs]"
    variable namdcommandOpt "+idlepoll +setcpuaffinity +p[::MembraneMixerPackage::getProcs]"
    grid [label  $f.run.namdlabelOpt -text "NAMD options:"] -row 2 -column 0 -sticky w
    grid [entry  $f.run.namdcommandOpt -width 30 -textvariable ::MembraneMixerPackage::namdcommandOpt] -row 2 -column 1 -columnspan 2 -sticky ew
    foreach l {"labelOpt" "commandOpt"} {::TKTOOLTIP::balloon $f.run.namd${l} "Specify options used to run NAMD."}
    
    grid columnconfigure $f.run 1 -weight 1
    pack $f.run -side top -padx 2 -pady 6 -expand 0 -fill both
    
    grid [button $f.button1 -text "RUN NOW!" -width 20 -state normal \
    -command {set ::MembraneMixerPackage::BuildScript 0; ::MembraneMixerPackage::run_exchange}]  -side top -padx 2 -pady 6 -expand 0 -fill both
    ::TKTOOLTIP::balloon $f.button1 "Start membrane generation in current VMD session."
    pack [button $f.button2 -text "Build script" -width 20 -state normal \
    -command {set ::MembraneMixerPackage::BuildScript 1; ::MembraneMixerPackage::prepareRunScript}]  -side top -padx 2 -pady 6 -expand 0 -fill both
    ::TKTOOLTIP::balloon $f.button2 "Prepare script to run Membrane Mixer later.\nThis is particularly useful if one wants to\nrun in another computer or in a cluster."
    
    
    # Statusbar
    frame $f.statusbar -bd 2 -relief ridge
    
    label $f.statusbar.label -text "IDLE" -anchor w
    pack  $f.statusbar.label -side left -fill both -padx 3 -pady 2 -expand 0 
    
    pack $f.statusbar -side top -fill both -expand 0
    
    
    # Console
    frame $f.console -bd 2 -relief ridge 
    set console $f.console
    ttk::treeview $console.log -selectmode none -yscrollcommand "$f.console.scroll set"
    $console.log configure -columns {num msg time} -show {} -height 4
    $console.log heading num -text "num"
    $console.log heading msg -text "msg"
    $console.log heading time -text "time"
    $console.log column num -width 30 -stretch 0 -anchor w
    $console.log column msg -width 100 -stretch 1 -anchor w
    $console.log column time -width 80 -stretch 0 -anchor e
    ttk::scrollbar $console.scroll -orient vertical -command "$f.console.log yview"
    
    grid $console -column 0 -row 1 -sticky nswe -padx 15 -pady "5 0"
    grid columnconfigure $console 0 -weight 1
    grid $console.log -column 0 -row 0 -sticky nswe
    grid $console.scroll -column 1 -row 0 -sticky nswe
    
    pack $f.console -side top -padx 2 -pady 6 -fill both -expand 0
    
    
    return $w
}

### OTHER PROCEDURES ###
proc ::MembraneMixerPackage::getProcs {} {
    global tcl_platform env
    if {$::tcl_platform(os) == "Darwin"} {
        catch {exec sysctl -n hw.ncpu} proce
        return $proce
    } elseif {$::tcl_platform(os) == "Linux"} {
        catch {exec grep -c "model name" /proc/cpuinfo} proce
        return $proce
    } elseif {[string first "Windows" $::tcl_platform(os)] != -1} {
        catch {HKEY_LOCAL_MACHINE\HARDWARE\DESCRIPTION\System\CentralProcessor } proce
        set proce [llength $proce]
        return $proce
    }
}

# proc ::MembraneMixerPackage::init_default_topology {} {
#     global env
#     variable parfiles
#     variable casesen
#     lappend parfiles [file join $env(CHARMMTOPDIR) top_all27_prot_lipid_na.inp]
# }
#
proc ::MembraneMixerPackage::psffileCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::psffile]
     if {$test_var eq ""} {
        $w.input.psfpath configure -background HotPink2
     } else {
        $w.input.psfpath configure -background white
     }
}

proc ::MembraneMixerPackage::pdbfileCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::pdbfile]
     if {$test_var eq ""} {
        $w.input.pdbpath configure -background HotPink2
     } else {
        $w.input.pdbpath configure -background white
     }
}

proc ::MembraneMixerPackage::conffileCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::conffile]
     if {$test_var eq ""} {
        $w.input.confpath configure -background HotPink2
     } else {
        $w.input.confpath configure -background white
     }
}

proc ::MembraneMixerPackage::outputpathCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::outputpath]
     if {$test_var eq ""} {
        $w.input.outputpath configure -background HotPink2
     } else {
        $w.input.outputpath configure -background white
     }
}

proc ::MembraneMixerPackage::lselectionCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::lselection]
     if {$test_var eq ""} {
        $w.selection.lselection configure -background HotPink2
     } else {
        $w.selection.lselection configure -background white
     }
}

proc ::MembraneMixerPackage::forceconstbilayerCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::forceconstbilayer]
     if {![string is double $test_var] || $test_var < 0} {
        $w.selection.forceconstbilayer configure -background HotPink2
     } else {
        $w.selection.forceconstbilayer configure -background white
     }
}

proc ::MembraneMixerPackage::forceconstCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::forceconst]
     if {![string is double $test_var] || $test_var < 0} {
        $w.selection.forceconst configure -background HotPink2
     } else {
        $w.selection.forceconst configure -background white
     }
}

proc ::MembraneMixerPackage::restrainedselectionCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::restrainedselection]
     if {$test_var eq ""} {
        $w.selection.restrainedselection configure -background HotPink2
     } else {
        $w.selection.restrainedselection configure -background white
     }
}

proc ::MembraneMixerPackage::exchangeCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::exchange]
     if {![string is double $test_var] || $test_var <= 0 || $test_var > 100} {
        $w.parameters.exchange configure -background HotPink2
     } else {
        $w.parameters.exchange configure -background white
     }
}

proc ::MembraneMixerPackage::nreplicasCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::nreplicas]
     if {![string is integer $test_var] || $test_var <= 0} {
        $w.parameters.nreplicas configure -background HotPink2
     } else {
        $w.parameters.nreplicas configure -background white
     }
}

proc ::MembraneMixerPackage::min1Check {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::num_min1]
     if {![string is integer $test_var] || $test_var < 0} {
        $w.moldyn.num_min1 configure -background HotPink2
     } else {
        $w.moldyn.num_min1 configure -background white
     }
}

proc ::MembraneMixerPackage::min2Check {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::num_min2]
     if {![string is integer $test_var] || $test_var < 0} {
        $w.moldyn.num_min2 configure -background HotPink2
     } else {
        $w.moldyn.num_min2 configure -background white
     }
}

proc ::MembraneMixerPackage::md1Check {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::num_md1]
     if {![string is integer $test_var] || $test_var < 0} {
        $w.moldyn.num_md1 configure -background HotPink2
     } else {
        $w.moldyn.num_md1 configure -background white
     }
}

proc ::MembraneMixerPackage::md2Check {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::num_md2]
     if {![string is integer $test_var] || $test_var < 0} {
        $w.moldyn.num_md2 configure -background HotPink2
     } else {
        $w.moldyn.num_md2 configure -background white
     }
}

proc ::MembraneMixerPackage::namdcommandCheck {args} {
    variable w
     set test_var [string trim $::MembraneMixerPackage::namdcommand]
     if {$test_var eq ""} {
        $w.run.namdcommand configure -background HotPink2
     } else {
        $w.run.namdcommand configure -background white
     }
}

proc ::MembraneMixerPackage::MoveScrollbar args {
    variable w
    #puts "yset args: $args"
    #puts "Scrollbar: [linsert $args 0 $iW.data.scrbar set]"
    eval [linsert $args 0 $w.console.scrbar set]
    [namespace current]::SetScollbar moveto [lindex [$w.console.scrbar get] 0]
}

proc ::MembraneMixerPackage::SetScollbar args {
    variable iResListboxArray
    foreach name [array names iResListboxArray] {
      eval [linsert $args 0 $iResListboxArray($name) yview]
    }
  
}

proc ::MembraneMixerPackage::UpdateStatusText { statusText } {
    variable w
    set path $w.statusbar.label
    $path configure -text $statusText
    update
}

proc ::MembraneMixerPackage::UpdateStateRun { state } {
    variable w
    set path1 $w.button1
    $path1 configure -state ${state}
    set path2 $w.button2
    $path2 configure -state ${state}
    update
}

proc ::MembraneMixerPackage::consoleMessage { desc } {
    # send a message to the console
    variable w
    # lookup and format some data
    set count [format "%03d" $::MembraneMixerPackage::consoleMessageCount]
    #set timestamp [clock format [clock seconds] -format {%m/%d/%Y- %I:%M:%S %p}]
    set timestamp [clock format [clock seconds] -format {%I:%M:%S %p}]

    # write the message to the console
    $w.console.log insert {} 0 -values [list $count $desc $timestamp]

    # increment the count
    incr ::MembraneMixerPackage::consoleMessageCount

    # if number of messages exceeds max, remove last node
    # this is important to prevent taking too much memory
    set itemList [$w.console.log children {}]
    if { [llength $itemList] > $::MembraneMixerPackage::consoleMaxHistory } {
            $w.console.log delete [lindex $itemList end]
    }
}

proc ::MembraneMixerPackage::write_state {args} {
    variable w
    if {$::MembraneMixerPackage::restrained == 0} {
    	$w.selection.restrainedselection configure  -state disabled
    	$w.selection.forceconst configure      -state disabled
    	$w.selection.forceconstlabel configure -state disabled
    	$w.selection.restrainedselectionlabel configure -state disabled
    
    } else {
    	$w.selection.restrainedselection configure  -state normal
    	$w.selection.forceconst configure      -state normal
    	$w.selection.forceconstlabel configure -state normal
    	$w.selection.restrainedselectionlabel configure -state normal
    
    }
}

proc ::MembraneMixerPackage::write_state_1 {args} {
    variable w
    if {$::MembraneMixerPackage::custom_conffile == 0} {
    	$w.input.conflabel  configure -state disabled
    	$w.input.confpath   configure -state disabled
    	$w.input.confbutton configure -state disabled
    
    } else {
    	$w.input.conflabel  configure -state normal
    	$w.input.confpath   configure -state normal
    	$w.input.confbutton configure -state normal
    }
}

proc ::MembraneMixerPackage::write_state_2 {args} {
    variable w
    if {$::MembraneMixerPackage::restrainedbilayer == 0} {
    	$w.selection.forceconstbilayerlabel  configure -state disabled
    	$w.selection.forceconstbilayer   configure -state disabled
    
    } else {
    	$w.selection.forceconstbilayerlabel  configure -state normal
    	$w.selection.forceconstbilayer   configure -state normal
    }
}

proc ::MembraneMixerPackage::ErrorMessCheck {TEXT} {
    puts "$TEXT"
    ::MembraneMixerPackage::UpdateStatusText "IDLE"
    ::MembraneMixerPackage::consoleMessage "ERROR DETECTED!"
    tk_messageBox -type ok -icon error -message "Action halted on error!" -detail "$TEXT" 
    ::MembraneMixerPackage::UpdateStateRun normal
    return -level 3
}

proc ::MembraneMixerPackage::ErrorMessWarn {TEXT} {
    puts "$TEXT"
    ::MembraneMixerPackage::consoleMessage "WARNING!"
    set answer [tk_messageBox -type yesno -icon warning -message "Warning!" -detail [concat $TEXT "\n Are you sure you want to continue?"]]
    case $answer {
	no  {
	    ::MembraneMixerPackage::UpdateStatusText "IDLE"
	    ::MembraneMixerPackage::UpdateStateRun normal
	    return -level 3
	    }
        yes {return} 
    }
}

proc ::MembraneMixerPackage::ErrorMessRun {TEXT} {
    variable ::MembraneMixerPackage::BuildScript
    puts2 "$TEXT"
    if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "ERROR DETECTED! SKIP THIS REPLICA!"}
    return 1
}

proc ::MembraneMixerPackage::puts2 {TEXT} {
    # Write some message to tk console and to progress file
    puts $TEXT
    set OUT [open $::MembraneMixerPackage::progressFile a]
    puts $OUT $TEXT
    close $OUT
}

proc ::MembraneMixerPackage::puts3 {opt TEXT} {
    # Write some message to tk console and to progress file
    puts $opt $TEXT
    set OUT [open $::MembraneMixerPackage::progressFile a]
    puts $opt $OUT $TEXT
    close $OUT
}

# Procedure to write text into a file
proc ::MembraneMixerPackage::TextWrite {FILE TEXT} {
    set out [open $FILE w]
    puts $out $TEXT
    close $out
}

# Procedure to append text into a file
proc ::MembraneMixerPackage::TextAppend {FILE TEXT} {
    set out [open $FILE a]
    puts $out $TEXT
    close $out
}

# Procedure to append content of a file into another file
proc ::MembraneMixerPackage::FileAppend {FILEIN FILEOUT} {
    set out [open $FILEOUT a]
    set in  [open $FILEIN r]
    while { ![eof $in] } {
        set inLine [gets $in]
        puts $out $inLine
    }
    close $out
    close $in
}

# Procedure that emulates GREP
proc ::MembraneMixerPackage::GrepEmu {pattern FILE begin} {
    # if begin == 1 then search pattern from beginning of line
    if {$begin} {
	set search "^$pattern"
    } else {
	set search "$pattern"
    }
    set line 1
    # Open require file to search pattern
    set fileID [open $FILE r]
    
    # This loop will get each line from given fileID, using regexp we search given pattern
    # on that line, displayed with corresponding line number and pattern on that line
    set match ""    
    set ind 0
    while { ![eof $fileID] } {
        gets $fileID content
	set trimmed [string trim $content]
        set res [regexp -nocase $search $trimmed]
        if {$res >= 1} {
	    set match $trimmed
	    set ind 1
        }
        incr line
    }
    close $fileID
    return [list $match $ind]
}

# Procedure to remove lines matching with patterns starting at the beginning of the line
proc ::MembraneMixerPackage::WriteRemoveLines {lisPattern FILE FILETMP} {
    # Open file
    set FILEIN [open $FILE r]
    set OUT [open $FILETMP w]
    # This loop will get each line from given FILE.
    # If a line matches the patter it is not written to the file
    gets $FILEIN content
    while { ![eof $FILEIN] } {
        set trimmed [string trim $content]
	set WriteLine 1
	foreach p $lisPattern {
            set res [regexp -nocase "^$p" $trimmed]
            if {$res} {
		set WriteLine 0
	    }
        }
	if {$WriteLine} {
	    puts $OUT $content
	}
        gets $FILEIN content
    }
    close $FILEIN
    close $OUT
    file copy -force $FILETMP $FILE
}

# Procedure to replace a line after matching a pattern at the beginning of the line
proc ::MembraneMixerPackage::WriteReplaceLine {Pattern NewLine FILE FILETMP} {
    # Open file
    set FILEIN [open $FILE r]
    set OUT [open $FILETMP w]
    # This loop will get each line from given FILE.
    # If a line matches the patter it will be replaced by the NewLine
    gets $FILEIN content
    while { ![eof $FILEIN] } {
        set trimmed [string trim $content]
        set res [regexp -nocase "^$Pattern" $trimmed]
        if {$res} {
            puts $OUT $NewLine
        } else {
            puts $OUT $content
	}
        gets $FILEIN content
    }
    close $FILEIN
    close $OUT
    file copy -force $FILETMP $FILE
}


# Procedure to add representation for piercing atoms
proc ::MembraneMixerPackage::add_rep_piercing {ser molid} {
    mol color Name
    mol representation VDW 0.8 12.0
    mol selection "serial $ser"
    mol material Opaque
    mol addrep $molid
    mol color ResName
    mol representation Licorice 0.1 12.0 12.0
    mol selection "same residue as(within 3 of (serial $ser))"
    mol material Opaque
    mol addrep $molid
}

# Procedure to set some representations
proc ::MembraneMixerPackage::add_rep_all {molid sel} {
#    color Display Background white
#    display nearclip set 0.0000
    mol showrep $molid 0 off
    mol color Resname
    mol selection $sel
    mol addrep $molid
    # Add representation for protein if present
    if {[[atomselect $molid "protein"] num]} {
        mol color segname
        mol representation NewCartoon
        mol selection "protein"
        mol addrep $molid
    }
}

# Procedure add representation for lipid residues
proc ::MembraneMixerPackage::add_rep_lipid {molid sel res} {
    mol color ResID
    mol representation Licorice 0.3 12.0 12.0
    mol selection "$sel and resid $res"
    mol material Opaque
    mol addrep $molid
}

# Procedure to check if there is ring piercing atoms
proc ::MembraneMixerPackage::CheckPiercing {id psf checkcutoff} {
    set listpiercing {}
    # Open psf file to read bond pairs
    set INP [open $psf r]
    set line [gets $INP]
    while { ![string match -nocase "*NBOND: bonds*" $line] } {
        set line [gets $INP]
    }
    set checkbond $checkcutoff
    while { ![string match -nocase "" $line] } {
        set line [gets $INP]
        for {set i 1} {$i<=[expr [llength $line]/2]} {incr i} {
            set b [measure bond [list [list [expr [lindex $line [expr $i*2-2]] - 1] $id]  [list [expr [lindex $line [expr $i*2-1]] - 1] $id] ]]
            if {$b>$checkbond && $b<15} {
                lappend listpiercing [list [lindex $line [expr $i*2-2]]  [lindex $line [expr $i*2-1]]]
                puts2 "   Bond between serial [lindex $line [expr $i*2-2]]  [lindex $line [expr $i*2-1]] is [format %.3f $b]: probable ring piercing."

            }
        }
    }
    close $INP
    if {![llength $listpiercing]} {puts2 "* No ring piercing with cutoff $checkcutoff Ang *"; return}
    # Check if there is a disulfide bond, which might have a bond longer than 2.0 A. Remove from the list in that case.
    set listfinal1 {}
    for {set i 0} {$i<[llength $listpiercing]} {incr i} {
        if {[string trim [[atomselect $id "serial [lindex $listpiercing $i 0]"] get type]] eq "SM" && [string trim [[atomselect $id "serial [lindex $listpiercing $i 1]"] get type]] eq "SM"} {
            puts2 "      Skipping serial [lindex $listpiercing $i 0]  [lindex $listpiercing $i 1] (atom type \"SM\"), it is a disulfide bond."
        } else {
            lappend listfinal1 [lindex $listpiercing $i]
        }
    }
    # Only select longer bond to move if there are many bonds related to the same ring piercing.
    # There might still be a problem if P-O bonds are piercing.
    set cent {} ;# calculate center of each bond
    for {set i 0} {$i<[llength $listfinal1]} {incr i} {
        lappend cent [measure center [atomselect $id "serial [lindex $listfinal1 $i 0] [lindex $listfinal1 $i 1]"]]
    }
    set Num [llength $cent]
    set group {}
    for {set i 0} {$i<$Num} {incr i} {
        lappend group "None"
    }
    set g 0
    for {set i 0} {$i<$Num} {incr i} {
        if {[lindex $group $i] ne "None"} {
            continue
        } else {
            set group [lreplace $group $i $i $g]
            set g [expr $g + 1]
        }
        unset -nocomplain dist
        for {set j 0} {$j<$Num} {incr j} {
               set dist($j) [vecdist [lindex $cent $i] [lindex $cent $j]]
        }
        for {set j 0} {$j<$Num} {incr j} {
            if {[expr abs($dist($i) - $dist($j))]<4.0} {
                set group [lreplace $group $j $j [lindex $group $i]]
            } 
         }   
    }
    # Find number of groups
    set NGroups [llength [lsort -unique $group]]
    # Make list with serial numbers subdivided by groups
    set lisGroups {}
    for {set i 0} {$i<$NGroups} {incr i} {
        lappend lisGroups {}
    }
    proc sub_lappend {listname idx args} {
        upvar 1 $listname l
        set subl [lindex $l $idx]
        lappend subl {*}$args
        lset l $idx $subl
    }
    for {set i 0} {$i<$NGroups} {incr i} {
        for {set j 0} {$j<$Num} {incr j} {
            if {[lindex $group $j] == $i } {
                sub_lappend lisGroups $i [lindex $listfinal1 $j]
            }
        }
    }
    set listfinal2 {}
    for {set i 0} {$i<$NGroups} {incr i} {
        set tempMax 0
        set indMax 0
        for {set j 0} {$j<[llength [lindex $lisGroups $i]]} {incr j} {
            set d [measure bond [list [list [expr [lindex $lisGroups $i $j 0] - 1] $id]  [list [expr [lindex $lisGroups $i $j 1] - 1] $id] ]]
            if {$d>$tempMax} {
                set tempMax $d
                set indMax $j
            }
        }
        lappend listfinal2 [lindex $lisGroups $i $indMax]
    }
    for {set i 0} {$i<[llength $listfinal2]} {incr i} {
        puts2 "   Bond between serial [lindex $listfinal2 $i 0]  [lindex $listfinal2 $i 1] is a ring piercing and will be fixed in next simulation."
    }
    if {![llength $listfinal2]} {puts2 "* No ring piercing with cutoff $checkcutoff Ang *"}
    return $listfinal2
}

# Get total amount of lipids in membrane
proc ::MembraneMixerPackage::GetLeafLipids {ID sel} {
    set AllLipID [lsort -unique -integer [[atomselect $ID $sel] get resid]]
    set NumLipTot [llength $AllLipID]
    # Get resid of lipids in each leaflet
    set Leaf1 {}
    set Leaf2 {}
    set membrange [measure minmax [atomselect $ID $sel]]
    set membZcenter [expr ([lindex $membrange 1 2] + [lindex $membrange 0 2])/2 ]
    foreach res $AllLipID {
        set comtmp [measure center [atomselect $ID "$sel and resid $res"] weight mass]
        if {[lindex $comtmp 2] >= $membZcenter} {
            lappend Leaf1 $res
        } else {
            lappend Leaf2 $res
        }
    }
    return [list $Leaf1 $Leaf2 $membZcenter]
}

# Procedure to get the lipids to exchange.
# Resid in list "li" will be exchanged pairwise with those in list "lf".
proc ::MembraneMixerPackage::get_res_leaflet {N lA} {
    set li {}
    set lf {}
    for {set i 0} {$i<[expr $N/2]} {incr i} {
        set randnum [expr int(rand()*[llength $lA])]
        set resid [lindex $lA $randnum]
        lappend li $resid
        set lA [lreplace $lA $randnum $randnum]
        set randnum [expr int(rand()*[llength $lA])]
        set resid [lindex $lA $randnum]
        lappend lf $resid
        set lA [lreplace $lA $randnum $randnum]
    }
    return [list $li $lf]
}

# Procedure to move the two atoms (and the atoms directly bound to them) away from the ring
proc ::MembraneMixerPackage::MovePiercing {ind1 ind2 molid c} {
    set a1 [atomselect $molid "index $ind1"]
    set a2 [atomselect $molid "index $ind2"]
    set ax [vecsub [lindex [$a2 get {x y z}] 0] [lindex [$a1 get {x y z}] 0]]; # vector defined by piercing atoms
    # Set a vector perpendiculat to "ax" using either the X or Y axis (choose randomly)
    if {[expr round(rand())]} {
        set norm1 [vecnorm [veccross $ax {1 0 0}]]
    } else {
        set norm1 [vecnorm [veccross $ax {0 1 0}]]
    }
    set norm2 [vecnorm [veccross $ax $norm1]]; # move atoms along this vector
    set listbonded [[atomselect $molid "serial $ind1 $ind2"] getbonds]
    # Move the group of piercing atoms in the positive or negative direction (choose randomly) by a certain amount
    if {$c<=5} {
        set step 3.0
    } elseif {$c>=10} {
        set step 3.4
    } else {
        set step 3.2
    }
    if {[expr round(rand())]} {
        set movestep -$step
    } else {
        set movestep $step
    }
    foreach i [lindex $listbonded 0] {
        display update ui
        [atomselect $molid "index $i"] moveby [vecscale $movestep $norm2]
    }
    foreach i [lindex $listbonded 1] {
        display update ui
        [atomselect $molid "index $i"] moveby [vecscale $movestep $norm2]
    }
}

# Procedure to add colvar to .col file
proc ::MembraneMixerPackage::AddColvar {inp name sel Z force} {
    puts $inp "colvar {"
    puts $inp "   name $name"
    puts $inp "   distanceZ {"
    puts $inp "      main {"
    puts $inp "         atomnumbers { [$sel get serial] }"
    puts $inp "      }"
    puts $inp "      ref {"
    puts $inp "         dummyAtom ( 0.000, 0.000, 0.000 )"
    puts $inp "      }"
    puts $inp "   }"
    puts $inp "}"
    puts $inp "harmonic {"
    puts $inp "   colvars $name"
    puts $inp "   centers       [format %.2f $Z]"
    puts $inp "   forceConstant $force"
    puts $inp "}"
}

# Procedure to exchange full center of mass or only XY (keep same Z) of the two lipids
proc ::MembraneMixerPackage::exchange_com {id sel res1 res2 ExProc} {
    set com1 [measure center [atomselect $id "$sel and resid $res1"] weight mass]
    set com2 [measure center [atomselect $id "$sel and resid $res2"] weight mass]
    set vec1 [vecsub $com2 $com1]
    set vec2 [vecsub $com1 $com2]
    if {$ExProc} {
        [atomselect $id "$sel and resid $res1"] moveby [list [lindex $vec1 0] [lindex $vec1 1] 0 ]
        [atomselect $id "$sel and resid $res2"] moveby [list [lindex $vec2 0] [lindex $vec2 1] 0 ]
    } else {
        [atomselect $id "$sel and resid $res1"] moveby $vec1
        [atomselect $id "$sel and resid $res2"] moveby $vec2
    }
}

# Procedure to start and follow a NAMD simulation run
proc ::MembraneMixerPackage::StartFollowNAMD {root conf NAMDPATH OPTS} {
    file delete ${root}.log
    eval ::ExecTool::exec \"${NAMDPATH}\" ${OPTS} \"${conf}\" > \"${root}.log\" &
    after 2000
    # Wait until the simulation is done
    set control [lindex [::MembraneMixerPackage::GrepEmu "End of program" ${root}.log 0] 1]
    puts3 -nonewline  "Waiting for NAMD job to finish..."
    set c 0
    while {!$control} {
        display update ui
        after 20
        incr c
        if {[expr {($c % 150) == 0}]} {
            puts3 -nonewline "."
            set control [lindex [::MembraneMixerPackage::GrepEmu "End of program" ${root}.log 0] 1]
            set control2 [lindex [::MembraneMixerPackage::GrepEmu "FATAL ERROR" ${root}.log 0] 1]
            # Check if fatal errors
            if {$control2} {
		set SKIPREP [::MembraneMixerPackage::ErrorMessRun "   A FATAL ERROR occurred during NAMD job! SKIPPING THIS REPLICA!"]
		return $SKIPREP
            }
        }
    }
    puts2 "Done."
    display update ui
    # Check if errors were found during simulations (e.g. Constraint failure)
    set control3 [lindex [::MembraneMixerPackage::GrepEmu "ERROR:" ${root}.log 0] 1]
    if {$control3} {
        puts2 "   AN ERROR WAS REPORTED IN NAMD LOG FILE! It might be related to ring piercing and might be corrected in next equilibration."
    }
    return 0
}

proc ::MembraneMixerPackage::WriteTMPConf {conftmp} {
    set OUT [open $conftmp w]
    puts $OUT "structure"
    puts $OUT "coordinates"
    puts $OUT "set temp           298"
    puts $OUT "dcdfreq            100"
    puts $OUT "outputEnergies     100"
    puts $OUT "xstFreq            100"
    puts $OUT "restartfreq        100"
    puts $OUT "outputTiming       10000"
    puts $OUT "paraTypeCharmm     on"
    puts $OUT "set path $::MembraneMixerPackage::dirCharmmPar"
    puts $OUT "parameters         \$path/par_all36_prot.prm"
    puts $OUT "parameters         \$path/par_all36_na.prm"
    puts $OUT "parameters         \$path/par_all36_carb.prm"
    puts $OUT "parameters         \$path/par_all36_lipid.prm"
    puts $OUT "parameters         \$path/par_all36_cgenff.prm"
    puts $OUT "parameters         \$path/toppar_water_ions_namd.str"
    puts $OUT "parameters         \$path/toppar_all36_carb_glycopeptide.str"
    puts $OUT "exclude            scaled1-4"
    puts $OUT "1-4scaling         1.0"
    puts $OUT "switching          on"
    puts $OUT "vdwForceSwitching  yes"
    puts $OUT "cutoff             12.0"
    puts $OUT "switchdist         10.0"
    puts $OUT "pairlistdist       16.0"
    puts $OUT "stepspercycle      20"
    puts $OUT "pairlistsPerCycle   2"
    puts $OUT "timestep           1.0"
    puts $OUT "rigidBonds         none"
    puts $OUT "nonbondedFreq      1"
    puts $OUT "fullElectFrequency 1"
    puts $OUT "wrapWater          on"
    puts $OUT "wrapAll            on"
    puts $OUT "PME                yes"
    puts $OUT "PMEInterpOrder     6"
    puts $OUT "PMEGridSpacing     1.0"
    puts $OUT "useGroupPressure       yes"
    puts $OUT "useFlexibleCell        yes"
    puts $OUT "useConstantRatio       yes"
    puts $OUT "langevinPiston         on"
    puts $OUT "langevinPistonTarget   1.01325"
    puts $OUT "langevinPistonTemp     \$temp"
    puts $OUT "langevin               on"
    puts $OUT "langevinDamping        1.0"
    puts $OUT "langevinTemp           \$temp"
    puts $OUT "langevinHydrogen       off"
    close $OUT
}

proc ::MembraneMixerPackage::MakeConfFile {ID wd psf pdb xsc root num_min num_md PHASE indPhase numrepF restrainedbilayer MembSelection membZcenter forceconstbilayer restrained restrGroupSelect forceconst conftmp filetmp ROOTPSF ChangeFreqOut parfiles} {
    variable ::MembraneMixerPackage::BuildScript
    # Make new input for simulation
    puts2 "Prepare conf file for simulation..."
    if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "Prepare CONF file (EQ${indPhase}-R$numrepF)"}
    set conf $root.conf
    file delete $conf
    if {$PHASE == "EQ1" || $PHASE == "RP"} {
        set min $num_min
        set md $num_md
        puts2 "   * Will minimize for $min steps"
        puts2 "   * Will run MD for $md steps"
        if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "MIN $min steps --> MD $md steps"}
    } elseif {$PHASE == "EQ2"} {
        set min $num_min
        set md $num_md
        puts2 "   * Will minimize for $min steps"
        puts2 "   * Will run MD for $md steps with \"mgridforcescale\" of 4"
        puts2 "   * Will run MD for $md steps with \"mgridforcescale\" of 3"
        puts2 "   * Will run MD for $md steps with \"mgridforcescale\" of 2"
        puts2 "   * Will run MD for $md steps with \"mgridforcescale\" of 1"
        puts2 "   * Will run MD for [expr 2*$md] steps with \"mgridforcescale\" of 0"
        if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "MIN $min steps --> MD [expr 6*$md] steps"}
    }
    if {$PHASE == "EQ1" || $PHASE == "EQ2"} {
	::MembraneMixerPackage::TextWrite $conf "\# Setup the grid \nmgridforce              yes\nmgridforcefile          a MMP.FORCEGRID.REP${numrepF}.REF${indPhase}.pdb\nmgridforcecol           a O\nmgridforcechargecol     a B\nmgridforcepotfile       a MMP.FORCEGRID.REP${numrepF}.REF${indPhase}.dx\nmgridforcescale         a 5.0 5.0 5.0\nmgridforcechecksize     a off\n"
    }
    if {$restrainedbilayer && [[atomselect $ID "$MembSelection and name P"] num]} {
        # Make colvar file to restraints membrane phosphate-phosphate distance.
        if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "Restrain membrane (EQ${indPhase}-R$numrepF)"}
        puts2 "Applying colvar to restrain membrane selection \"$MembSelection\""
        ::MembraneMixerPackage::TextAppend $conf "\# Setup colvar to restrain membrane. Same file for all simulations.\ncolvars                 on\ncolvarsConfig           MMP.${ROOTPSF}.REP${numrepF}.EQ1.col\n"
        if {$PHASE == "EQ1"} {
            puts2 "Applying colvar restraints on phosphorus of phospholipids in selection \"$MembSelection\""
            set selP1 [atomselect $ID "$MembSelection and name P and z > $membZcenter"]
            set selP2 [atomselect $ID "$MembSelection and name P and z < $membZcenter"]
            set ZP1 [expr [lindex [measure center $selP1] 2] - $membZcenter]; # equilibrium value for distance in colvar, upper leaflet
            set ZP2 [expr [lindex [measure center $selP2] 2] - $membZcenter]; # equilibrium value for distance in colvar, lower leaflet
            set inpcol [open $root.col w]
            puts $inpcol ""
            puts $inpcol "Colvarstrajfrequency    [expr $min/2]"
            puts $inpcol ""
            ::MembraneMixerPackage::AddColvar $inpcol "RestrPhosUpper" $selP1 $ZP1 $forceconstbilayer
            ::MembraneMixerPackage::AddColvar $inpcol "RestrPhosLower" $selP2 $ZP2 $forceconstbilayer
            # Check if cholesterol is present and in that case add colvar to oxygen atom
            if {[[atomselect $ID "resname CHL1"] num]} {
                puts2 "Applying colvar restraints on oxygen of cholesterol in selection \"$MembSelection\""
                set selC1 [atomselect $ID "$MembSelection and resname CHL1 and name O3 and z > $membZcenter"]
                set selC2 [atomselect $ID "$MembSelection and resname CHL1 and name O3 and z < $membZcenter"]
                set ZC1 [expr [lindex [measure center $selC1] 2] - $membZcenter]; # equilibrium value for distance in colvar, upper leaflet
                set ZC2 [expr [lindex [measure center $selC2] 2] - $membZcenter]; # equilibrium value for distance in colvar, lower leaflet
                ::MembraneMixerPackage::AddColvar $inpcol "RestrCholUpper" $selC1 $ZC1 $forceconstbilayer
                ::MembraneMixerPackage::AddColvar $inpcol "RestrCholLower" $selC2 $ZC2 $forceconstbilayer
            }
            close $inpcol
        }
    } elseif {$restrainedbilayer && ![[atomselect $ID "$MembSelection and name P"] num]} {
        if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "WARNING: No phosphorous atoms in \"$MembSelection\"!"}
        puts2 "WARNING: No phosphorous atoms found for \"$MembSelection\" selection! Not applying membrane restrain!"
    }
    if {$restrained && [[atomselect $ID "$restrGroupSelect"] num]} {
        # Fix position of some group of atoms if requested. The equilibrium positionis are always that of the initial structure (EQ1)
        puts2 "Restraining position of selection \"$restrGroupSelect\""
        if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "Restrain selection \"$restrGroupSelect\" (EQ${indPhase}-R$numrepF)"}
        ::MembraneMixerPackage::TextAppend $conf "\# Restraints. Same file for Equilibration 1 and 2.\nconstraints             on\nconsexp                 2\nconsref                 MMP.RESTRAIN.REP${numrepF}.REF1.pdb\nconskfile               MMP.RESTRAIN.REP${numrepF}.REF1.pdb\nconskcol                B\nconstraintScaling       1.0\n"
        if {$PHASE == "EQ1"} {
            # Write the pdb file only in Equilibration 1, as the same file will be used in later simulations
            [atomselect $ID all] set occupancy 0
            [atomselect $ID all] set beta 0
            [atomselect $ID "$restrGroupSelect"] set beta $forceconst
            [atomselect $ID all] writepdb [file join $wd MMP.RESTRAIN.REP${numrepF}.REF1.pdb]
        }
    } elseif {$restrained && ![[atomselect $ID "$restrGroupSelect"] num]} {
        if {!$::MembraneMixerPackage::BuildScript} {::MembraneMixerPackage::consoleMessage "WARNING: No atoms found \"$restrGroupSelect\""}
        puts2 "WARNING: No atoms found for \"$restrGroupSelect\" selection! Not applying group restrain!"
    }
    # Add other important settings in conf file
    if {$ChangeFreqOut} {set freqEQ $ChangeFreqOut}
    if {$PHASE == "EQ1"} {
        ::MembraneMixerPackage::TextAppend $conf "extendedsystem          ${xsc}\noutputname              [file tail ${root}]\ntemperature             300\nmargin                  4\nlangevinPistonPeriod    300.0\nlangevinPistonDecay     200.0\n"
        if {$ChangeFreqOut} {::MembraneMixerPackage::TextAppend $conf "dcdfreq                 ${freqEQ}\noutputEnergies          ${freqEQ}\nxstFreq         	${freqEQ}\nrestartfreq     	${freqEQ}\n"}
        ::MembraneMixerPackage::FileAppend $conftmp $conf
	if {[llength $parfiles]} {::MembraneMixerPackage::AddParFiles $conf $parfiles}
        ::MembraneMixerPackage::TextAppend $conf "minimize                $min\nrun                     $md\n"
        ::MembraneMixerPackage::WriteReplaceLine "coordinates" "coordinates             $pdb" $conf $filetmp
    } elseif {$PHASE == "EQ2"} {
	set eq1root MMP.${ROOTPSF}.REP${numrepF}.EQ1
        ::MembraneMixerPackage::TextAppend $conf "binvelocities           [file tail ${eq1root}].restart.vel\nextendedsystem          [file tail ${eq1root}].restart.xsc\noutputname              [file tail ${root}]\nmargin                  4\nlangevinPistonPeriod    300.0\nlangevinPistonDecay     200.0\n"
        if {$ChangeFreqOut} {::MembraneMixerPackage::TextAppend $conf "dcdfreq                 ${freqEQ}\noutputEnergies          ${freqEQ}\nxstFreq         	${freqEQ}\nrestartfreq     	${freqEQ}\n"}
        ::MembraneMixerPackage::FileAppend $conftmp $conf
	if {[llength $parfiles]} {::MembraneMixerPackage::AddParFiles $conf $parfiles}
        ::MembraneMixerPackage::TextAppend $conf "minimize                $min\nset scale 5\nfor {set i 0} {\$i<5} {incr i} {\n    set scale \[expr \$scale - 1\]\n    updateGridScale     a \$scale \$scale \$scale\n    run                 $md\n}\nupdateGridScale     a 0 0 0\nrun                 $md\n"
        ::MembraneMixerPackage::WriteReplaceLine "coordinates" "coordinates             MMP.FORCEGRID.REP${numrepF}.REF2.pdb" $conf $filetmp
    } elseif {$PHASE == "RP"} {
        set restroot MMP.${ROOTPSF}.REP${numrepF}.EQ[expr ${indPhase} - 1]
        ::MembraneMixerPackage::TextAppend $conf "binvelocities           ${restroot}.restart.vel\nextendedsystem          ${restroot}.restart.xsc\noutputname              [file tail ${root}]\nmargin                  4\nlangevinPistonPeriod    300.0\nlangevinPistonDecay     200.0\n"
        if {$ChangeFreqOut} {::MembraneMixerPackage::TextAppend $conf "dcdfreq                 ${freqEQ}\noutputEnergies          ${freqEQ}\nxstFreq         	${freqEQ}\nrestartfreq     	${freqEQ}\n"}
        ::MembraneMixerPackage::FileAppend $conftmp $conf
	if {[llength $parfiles]} {::MembraneMixerPackage::AddParFiles $conf $parfiles}
        ::MembraneMixerPackage::TextAppend $conf "minimize                $min\nrun                     $md\n"
	[atomselect $ID all] writepdb [file join $wd MMP.COORD.REP${numrepF}.REF${indPhase}.pdb]
        ::MembraneMixerPackage::WriteReplaceLine "coordinates" "coordinates             MMP.COORD.REP${numrepF}.REF${indPhase}.pdb" $conf $filetmp
    }
    ::MembraneMixerPackage::WriteReplaceLine "structure" "structure               $psf" $conf $filetmp
    return $conf
}

proc ::MembraneMixerPackage::AddParFiles {conf parfiles} {
    ::MembraneMixerPackage::TextAppend $conf "# Additional parameter files"
    for {set i 0} {$i<[llength $parfiles]} {incr i} {
	::MembraneMixerPackage::TextAppend $conf "parameters              [lindex $parfiles $i]"
    }
}

proc ::MembraneMixerPackage::MoveSimulationFiles {DIR TAG REP} {
    # Move files into the proper directory after simulation.
    set lisFileMove {}
    cd [file dirname $DIR]
    foreach NAME [list "$TAG" "MMP.FORCEGRID.REP${REP}" "MMP.COORD.REP${REP}" "MMP.RESTRAIN.REP${REP}"] {
        if {![catch {glob -type f ${NAME}*}]} {
            set lisTMP [glob -type f ${NAME}*]
            set lisFileMove [concat $lisFileMove $lisTMP]
        }
    }
    if {[llength $lisFileMove]} {
        file mkdir $DIR
        foreach f $lisFileMove {
            file rename -force $f $DIR
        }
    }
}

proc ::MembraneMixerPackage::GuessLipidSel {} {
        set psf  $::MembraneMixerPackage::psffile
        set pdb  $::MembraneMixerPackage::pdbfile
        # Checking initial settings and files
        ::MembraneMixerPackage::UpdateStatusText "Guess lipid selection"
        ::MembraneMixerPackage::consoleMessage "Guess lipid selection"
	# Check if PSF and PDB exist
        foreach f [list $psf $pdb] tag [list "PSF file" "PDB file"] {
            if {[string trim $f] eq ""} {
                ::MembraneMixerPackage::ErrorMessCheck "Error: \"$tag\" variable is empty!"
            }
        }
        foreach f [list $psf $pdb] tag [list "PSF file" "PDB file"] {
            if {[file exists $f] == 0} {
                    ::MembraneMixerPackage::ErrorMessCheck "Error: \"$tag\" does not exist!"
            }
        }
        # Load molecular system
        mol new $psf
        mol addfile $pdb
        set ID [molinfo top]
	# Check if molecule was correctly loaded
	if {![molinfo $ID get numframes]} {
            ::MembraneMixerPackage::ErrorMessCheck "Something is wrong with your structure!\nNo loaded molecule."
	}
	# Find possible lipids
	set RESLIP "resname DLPE DMPC DPPC GPC LPPC PALM PGCL POPC POPE DOPC CHL1 PSM POPA DPPA DSPA DMPA DLPA PLPA SOPA SLPA DYPA YOPA DOPA DGPA DEPA DNPA \
			    LLPA DDPC DCPC DLPC DSPC PSPC PLPC PAPC SOPC SLPC DRPC DYPC YOPC DUPC DGPC DEPC DNPC DLIPC LLPC DMPE DPPE DSPE PYPE DSPG PYPG \
			    PLPE SOPE SLPE DYPE YOPE OYPE DOPE DGPE DEPE DNPE DLIPE LLPE DLPG DMPG DPPG POPG PLPG SOPG SLPG DYPG DOPG DGPG DEPG DNPG DLPS \
			    DMPS DPPS DSPS POPS PLPS SOPS SLPS OLPS DYPS TOPS DOPS DGPS DEPS DNPS LLPS POPP1 POPP2 POPP3 DOPP1 DOPP2 DOPP3 DMPI DMPI13 DMPI14 \
			    DMPI15 DMPI24 DMPI25 DMPI2A DMPI2B DMPI2C DMPI2D DMPI33 DMPI34 DMPI35 PYPI POPI POPI13 POPI14 POPI15 POPI24 POPI25 POPI2A POPI2B \
			    POPI2C POPI2D POPI33 POPI34 POPI35 PLPI PLPI13 PLPI14 PLPI15 PLPI24 PLPI2A PLPI2B PLPI2C PLPI2D PLPI33 PLPI34 PLPI35 PNPI PNPI13 \
   			    PNPI14 PNPI15 PNPI24 PNPI25 PNPI2A PNPI2B PNPI2C PNPI2D PNPI33 PNPI34 PNPI35 SAPI SAPI13 SAPI14 SAPI15 SAPI24 SAPI25 SAPI2A SAPI2B \
			    SAPI2C SAPI2D SAPI33 SAPI34 SAPI35 DLIPI TMCL1 TMCL2 PMCL1 PMCL2 PVCL2 TYCL1 TYCL2 TOCL1 TOCL2 LOACL1 LOACL2 LOCCL1 LOCCL2 TLCL1 \
			    TLCL2 LNCCL1 LNCCL2 LNACL1 LNACL2 LNDCL1 LNDCL2 LNBCL1 LNBCL2 DMTAP DPTAP DOTAP DSTAP PDOPC PDOPE SAPA SAPC SAPE SAPG SAPS SDPA SDPC \
			    SDPE SDPG SDPS DAPA DAPC DAPE DAPG DAPS DDOPC DDOPE DDOPS TSPC SSM ASM BSM 23SM LSM OSM NSM CER160 CER180 CER181 CER200 CER220 CER240 \
			    CER241 CER3E CER6 CER3 DHPCE DMPCE DMPEE DOPCE DOPEE DPPEE POPCE POPEE QMPE PMPE PMPG PPPE PYPE PVPG APPC IPPC PHPC DPPGK BMGP LAU \
			    MYR PAL STE ARA BEH TRI LIGN MYRO PALO HTA OLE LIN ALIN SDA GLA EICO EDA MEA DGLA ETE ETA EPA ARAN HPA ERU DDA ADR DPT DPA DHA NER \
			    TTA TPT TPA THA LAUP MYRP  PALP STEP AREP BEHP TRIP LIGNP MYROP PALOP HTAP OLEP LINP ALINP SDAP GLAP EICOP EDAP MEAP DGLAP ETEP \
			    EPAP ARANP HPAP ERUP DDAP ADRP DPTP DPAP DHAP NERP TTAP TPTP TPAP THAP CHAPS CHAPSO DDMG DOMG DDAO UDAO LDAO BUDTM ADDTM BDDTM \
			    LAPAO TRIPAO FOS10 FOS12 FOS13 FOS14 FOS15 FOS16 UFOS10 LPC12 LPC14 FOIS9 FOIS11 C6DHPC C7DHPC CYFOS3 CYFOS4 CYFOS5 CYFOS6 CYFOS7 \
			    SDS LMPG LPPG DDAOP UDAOP LDAOP LAPAOP TRPAOP AOG BOG ANG BNG ADG BDG ADDG BDDG ACY3G BCY3G AMHCG BMHCG AOM BOM ANM BNM ADM BDM \
			    AUDM BUDM A2UDM B2UDM ADDM BDDM ADMHM BDMHM APPM BPPM ATM BTM ACY3M BCY3M ACY4M BCY4M ACY5M BCY5M ACY6M BCY6M ACY7M BCY7M NHEG \
			    DHEG NMG DMG SMDD BBPHG BBCYG BOGNG BOTGNG BOING BLMNG BDMNG C5MNG C6MNG AHTG BHTG AOTG BOTG AOTM BOTM ANTM BNTM ADTM BDTM AUDTM "
	set GUESSED {}
        if {![[atomselect $ID $RESLIP] num]} {
            ::MembraneMixerPackage::ErrorMessCheck "No lipids could be detected automatically! Please choose a valid lipid selection."
        } else {
	    set seg [lsort -unique [[atomselect top "$RESLIP"] get segname]]
	    set GUESSED "segname $seg"
            ::MembraneMixerPackage::UpdateStatusText "IDLE"
	    ::MembraneMixerPackage::add_rep_all $ID $GUESSED
	    return $GUESSED
	}
}

proc ::MembraneMixerPackage::SanityCheck {} {
       # Set variables in current namespace
        set psf  $::MembraneMixerPackage::psffile
        set pdb  $::MembraneMixerPackage::pdbfile
        set conf $::MembraneMixerPackage::conffile
        set MembSelection $::MembraneMixerPackage::lselection
        set restrGroupSelect $::MembraneMixerPackage::restrainedselection
        set custom_conffile $::MembraneMixerPackage::custom_conffile
        set namdcommand $::MembraneMixerPackage::namdcommand
        set namdcommandOpt $::MembraneMixerPackage::namdcommandOpt
        set restrainedbilayer $::MembraneMixerPackage::restrainedbilayer
        set forceconstbilayer $::MembraneMixerPackage::forceconstbilayer
        set restrained $::MembraneMixerPackage::restrained
        set forceconst $::MembraneMixerPackage::forceconst
        set exchange $::MembraneMixerPackage::exchange
        set nreplicas $::MembraneMixerPackage::nreplicas
        set num_min1 $::MembraneMixerPackage::num_min1
        set num_md1 $::MembraneMixerPackage::num_md1
        set num_min2 $::MembraneMixerPackage::num_min2
        set num_md2 $::MembraneMixerPackage::num_md2
        set ExProc $::MembraneMixerPackage::ExProc
        set outputpath $::MembraneMixerPackage::outputpath
	variable ::MembraneMixerPackage::ChangeFreqOut 0

	# Checking initial settings and files
        ::MembraneMixerPackage::UpdateStatusText "Checking..."
        ::MembraneMixerPackage::consoleMessage "Check settings and files"
        foreach f [list $psf $pdb $namdcommand $exchange $nreplicas $MembSelection $num_min1 $num_md1 $num_min2 $num_md2 $outputpath] tag [list "PSF file" "PDB file" "NAMD path" "Lipid exchange" "Number of replica" "Lipid selection" "Min. steps (first eq.)" "MD. steps (first eq.)" "Min. steps (second eq.)" "MD. steps (second eq.)" "Output path"] {
            if {[string trim $f] eq ""} {
                ::MembraneMixerPackage::ErrorMessCheck "Error: \"$tag\" variable is empty!"
            }
        }
        if {$restrainedbilayer} {
            if {[string trim $forceconstbilayer] eq ""} {
                ::MembraneMixerPackage::ErrorMessCheck "Error: \"Force constant (bilayer)\" variable is empty!"
            }
            if {$forceconstbilayer <= 0} {::MembraneMixerPackage::ErrorMessCheck "Error: force constant for phosphate-phospate distance colvar must be higher than 0!"}
            if {![string is double $forceconstbilayer]} {::MembraneMixerPackage::ErrorMessCheck "Error: force constant for phosphate-phospate distance colvar is not a floating-point value!"}
        }
        if {$restrained} {
            foreach f [list $forceconst $restrGroupSelect] tag [list "Force constant (group)" "Group selection"] {
                if {[string trim $f] eq ""} {
                    ::MembraneMixerPackage::ErrorMessCheck "Error: \"$tag\" variable is empty!"
                }
            }
            if {$forceconst <= 0} {::MembraneMixerPackage::ErrorMessCheck "Error: force constant for group restrain must be higher than 0!"}
            if {![string is double $forceconst]} {::MembraneMixerPackage::ErrorMessCheck "Error: force constant for group restrain is not a floating-point value!"}
        }

        foreach f [list $psf $pdb] tag [list "PSF file" "PDB file"] {
            if {[file exists $f] == 0} {
                    ::MembraneMixerPackage::ErrorMessCheck "Error: \"$tag\" does not exist!"
            }
        }

        foreach f [list $nreplicas $num_min1 $num_md1 $num_min2 $num_md2] tag [list "Number of replica" "Min. steps (first eq.)" "MD. steps (first eq.)" "Min. steps (second eq.)" "MD. steps (second eq.)"] {
            if {![string is integer [string trim $f]] || $f < 0} {
                ::MembraneMixerPackage::ErrorMessCheck "Error: \"$tag\" is not a positive integer!"
            }
        }
        if {$exchange > 100 || $exchange <= 0} {::MembraneMixerPackage::ErrorMessCheck "Error: lipid exchange percentage must be higher than 0% and lower than 100%!"}
        if {$nreplicas <= 0 || ![string is integer $nreplicas]} {::MembraneMixerPackage::ErrorMessCheck "Error: specify a valid number for number of replica to generate (\"Numer of replicas\" variable)"}
        if {$custom_conffile} {
            if {[string trim $conf] eq "" || [file exists $conf] == 0} {
                ::MembraneMixerPackage::ErrorMessCheck "Error: configuration file variable is empty or the file does not exist!"
            }
            # Check if minimization or MD steps are multiple of restart and dcd output frequency 
            set grep1 [lindex [::MembraneMixerPackage::GrepEmu "restartfreq" $conf 1] 0]
            set grep2 [lindex [::MembraneMixerPackage::GrepEmu "dcdfreq" $conf 1] 0]
            if {[lindex $grep1 0] == "" || [lindex $grep1 1] == "" } {::MembraneMixerPackage::ErrorMessCheck "Conf file does not contain \"restartfreq\" keyword, which is needed in the simulation!"}
            if {[lindex $grep2 0] == "" || [lindex $grep2 1] == "" } {::MembraneMixerPackage::ErrorMessCheck "Conf file does not contain \"dcdfreq\" keyword, which is needed in the simulation!"}
            set confrest [string trim [lindex $grep1 1] ";"]
            set confdcd [string trim [lindex $grep2 1] ";"]
            foreach v [list $confrest $confdcd] t1 [list "\"restartfreq\" in conf file" "\"dcdfreq\" in conf file"] {
                foreach e [list $num_min1 $num_md1 $num_min2 $num_md2] t2 [list "minsteps1" "runsteps1" "minsteps2" "runsteps2"] {
                    if {$v>$e} { 
                        #::MembraneMixerPackage::ErrorMessCheck "Frequency for $t1 is higher than \"$t2\" variable set in this script!"
			set listmp [concat $num_min1 $num_md1 $num_min2 $num_md2]
			set ::MembraneMixerPackage::ChangeFreqOut [expr int([tcl::mathfunc::min {*}$listmp]/2)]
                        puts "Output frequency $t1 is higher than \"$t2\" variable. Setting all output frequency to $::MembraneMixerPackage::ChangeFreqOut!"
                    }
                    if {[expr int([expr {fmod($e,$v)}])]} {
                        #::MembraneMixerPackage::ErrorMessCheck "\"$t2\" variable must be a multiple of frequency for $t1!"
			set listmp [concat $num_min1 $num_md1 $num_min2 $num_md2]
			set ::MembraneMixerPackage::ChangeFreqOut [expr int([tcl::mathfunc::min {*}$listmp]/2)]
                        puts "Variable \"$t2\" is not a multiple of output frequency $t1. Setting all output frequencies to $::MembraneMixerPackage::ChangeFreqOut!"
                    }
                }
            }
        }
        # Check if membrane selection is empty
        mol new $psf
        mol addfile $pdb
        set IDi [molinfo top]
        if {![[atomselect $IDi "$MembSelection"] num]} {
            ::MembraneMixerPackage::ErrorMessCheck "No atoms found for \"$MembSelection\"! \nCannot continue without a valid \nmembrane selection."
        }
        # Get total amount of lipids in each leaflet and check if there are at least two resid per leaflet
        set listmp [::MembraneMixerPackage::GetLeafLipids $IDi $MembSelection]
	if {[llength [lindex $listmp 0]] < 2 || [llength [lindex $listmp 1]] < 2} {
            ::MembraneMixerPackage::ErrorMessCheck "There are less than 2 residue ID in \"$MembSelection\" \nper leaflet! Not enough to exchange."
	}
	# Check if phosphorous exist in selection
        if {$restrainedbilayer && ![[atomselect $IDi "$MembSelection and name P"] num]} {
	    ::MembraneMixerPackage::ErrorMessWarn "No phosphorous atoms in \"$MembSelection\"!"
        }
	# Check if atoms exist in Group selection
        if {$restrained && ![[atomselect $IDi "$restrGroupSelect"] num]} {
            ::MembraneMixerPackage::ErrorMessWarn "No atoms found \"$restrGroupSelect\"!"
        }
        # Check is progress file is already present, overwrite previous simulations?
        set tmpdir [file dirname $outputpath]
        set tmptail [file tail $outputpath]
        set wd_master [file join $tmpdir [regsub {[ ]} $tmptail {} ]]
        variable progressFile [file join $wd_master "MMP.PROGRESS.txt"]
        if {[file exists $progressFile]} {
	    ::MembraneMixerPackage::ErrorMessWarn "The progress file exist already! \nIf you continue previous simulations might be overwritten." 
        }
}

proc ::MembraneMixerPackage::prepareRunScript {} {
	::MembraneMixerPackage::UpdateStateRun disabled
	# Create a tcl script to run this plugin in VMD text mode
        # Some initial checkings
        ::MembraneMixerPackage::SanityCheck 
	# The script is written in the current directory.
        ::MembraneMixerPackage::UpdateStatusText "Writing..."
	::MembraneMixerPackage::consoleMessage "Build run script."
	set ScriptName [file join [pwd] "MMP.RunScript.tcl"]
	set OUT [open $ScriptName w]
	puts $OUT "# Generated by Membrane Mixer plugin."
	puts $OUT "# Run through VMD text mode."
	puts $OUT "# E.g.: vmd -dispdev text -e \"MMP.RunScript.tcl\""
	puts $OUT ""
	#puts $OUT "# Source plugin, REMOVE THESE LINES LATER!"
	#puts $OUT "if \{1\} \{"
	#puts $OUT "    source  /Projects/giuseppe/tests/MakeMembReplica/MakeMembraneReplica.tcl"
	#puts $OUT "\}"
	puts $OUT "# Require Membrane Mixer package"
	puts $OUT "package require membranemixerpackage"
	puts $OUT "# SET VARIABLES"
	puts $OUT "# Variables were checked when this script was generated through the GUI."
	puts $OUT "# At this point, the plugin does not check them anymore. Be careful what you change here."
	puts $OUT "set ::MembraneMixerPackage::psffile  \"$::MembraneMixerPackage::psffile\""
	puts $OUT "set ::MembraneMixerPackage::pdbfile  \"$::MembraneMixerPackage::pdbfile\""
	puts $OUT "set ::MembraneMixerPackage::conffile \"$::MembraneMixerPackage::conffile\""
	puts $OUT "set ::MembraneMixerPackage::custom_conffile $::MembraneMixerPackage::custom_conffile"
	puts $OUT "set ::MembraneMixerPackage::parfiles {$::MembraneMixerPackage::parfiles}"
	puts $OUT "set ::MembraneMixerPackage::outputpath \"$::MembraneMixerPackage::outputpath\""
	puts $OUT "set ::MembraneMixerPackage::restrainedbilayer $::MembraneMixerPackage::restrainedbilayer"
	puts $OUT "set ::MembraneMixerPackage::lselection \"$::MembraneMixerPackage::lselection\""
	puts $OUT "set ::MembraneMixerPackage::forceconstbilayer $::MembraneMixerPackage::forceconstbilayer"
	puts $OUT "set ::MembraneMixerPackage::restrained $::MembraneMixerPackage::restrained"
	puts $OUT "set ::MembraneMixerPackage::restrainedselection \"$::MembraneMixerPackage::restrainedselection\""
	puts $OUT "set ::MembraneMixerPackage::forceconst $::MembraneMixerPackage::forceconst"
	puts $OUT "set ::MembraneMixerPackage::exchange $::MembraneMixerPackage::exchange"
	puts $OUT "set ::MembraneMixerPackage::nreplicas $::MembraneMixerPackage::nreplicas"
	puts $OUT "set ::MembraneMixerPackage::ExProc $::MembraneMixerPackage::ExProc"
	puts $OUT "set ::MembraneMixerPackage::num_min1 $::MembraneMixerPackage::num_min1"
	puts $OUT "set ::MembraneMixerPackage::num_md1 $::MembraneMixerPackage::num_md1"
	puts $OUT "set ::MembraneMixerPackage::num_min2 $::MembraneMixerPackage::num_min2"
	puts $OUT "set ::MembraneMixerPackage::num_md2 $::MembraneMixerPackage::num_md2"
	puts $OUT "set ::MembraneMixerPackage::namdcommand \"$::MembraneMixerPackage::namdcommand\""
	puts $OUT "set ::MembraneMixerPackage::namdcommandOpt \"$::MembraneMixerPackage::namdcommandOpt\""
	puts $OUT "set ::MembraneMixerPackage::BuildScript 1"
	puts $OUT "set ::MembraneMixerPackage::ChangeFreqOut $::MembraneMixerPackage::ChangeFreqOut"
	puts $OUT "# Run plugin from command line"
	puts $OUT "::MembraneMixerPackage::run_exchange"
	puts $OUT ""
	close $OUT
	::MembraneMixerPackage::consoleMessage "Written in: [pwd]"
        ::MembraneMixerPackage::UpdateStatusText "IDLE"
        ::MembraneMixerPackage::UpdateStateRun normal
}

### MAIN PROCEDURE: run exchage of lipids
proc ::MembraneMixerPackage::run_exchange {} {
	set script $::MembraneMixerPackage::BuildScript
	if {!$script} {::MembraneMixerPackage::UpdateStateRun disabled}
	# Set variables in current namespace
	set psf  "$::MembraneMixerPackage::psffile"
	set pdb  "$::MembraneMixerPackage::pdbfile"
	set conf "$::MembraneMixerPackage::conffile"
	set MembSelection $::MembraneMixerPackage::lselection
	set restrGroupSelect $::MembraneMixerPackage::restrainedselection
	set custom_conffile $::MembraneMixerPackage::custom_conffile
	set parfiles $::MembraneMixerPackage::parfiles
        set namdcommand $::MembraneMixerPackage::namdcommand
        set namdcommandOpt $::MembraneMixerPackage::namdcommandOpt
        set restrainedbilayer $::MembraneMixerPackage::restrainedbilayer
        set forceconstbilayer $::MembraneMixerPackage::forceconstbilayer
        set restrained $::MembraneMixerPackage::restrained
        set forceconst $::MembraneMixerPackage::forceconst
        set exchange $::MembraneMixerPackage::exchange
        set nreplicas $::MembraneMixerPackage::nreplicas
        set num_min(1) $::MembraneMixerPackage::num_min1
        set num_md(1) $::MembraneMixerPackage::num_md1
        set num_min(2) $::MembraneMixerPackage::num_min2
        set num_md(2) $::MembraneMixerPackage::num_md2
        set ExProc $::MembraneMixerPackage::ExProc
        set outputpath $::MembraneMixerPackage::outputpath
	# Grid-force settings
        set resolution 6
        set spacing 0.8
        # Initialize list of all molID
        set lisMolID {}
	# Set variable to skip a replica in case NAMD crashes
	set SKIPREP 0
	# Follow number of membranes generated and skipped
	set lisGenerated {}
	set lisSkipped {}

	# Some initial checkings
	if {!$script} {::MembraneMixerPackage::SanityCheck}
	set ChangeFreqOut $::MembraneMixerPackage::ChangeFreqOut

	# Set master working directory. Remove spaces in path name.
	set tmpdir [file dirname $outputpath]
	set tmptail [file tail $outputpath]
	set wd_master [file join $tmpdir [regsub {[ ]} $tmptail {} ]]
	file mkdir $wd_master
	# Set txt file to output progresses of plugin
	variable progressFile [file join $wd_master "MMP.PROGRESS.txt"]
	file delete $progressFile
	#Set a root name from psf file, avoid spaces in the path
	set ROOTPSF [regsub {[ ]} [file rootname [file tail $psf]] {} ]

        puts2 "################################################################################"
        puts2 "########################   MAKE  MEMBRANE  REPLICA  ############################"
        puts2 "################################################################################"

	# Load some packages
	puts3 -nonewline "Loading required packages..."
	package require pbctools
	package require chirality
	package require cispeptide
	package require mdff
	puts2 "Done."

	# Make temporary configuration file
	puts3 -nonewline "Making temporary NAMD configuration file..."
	set conftmp [file join $wd_master "MMP.CONFTMP.conf"]
	set filetmp [file join $wd_master "MMP.FILETMP.conf"]
	if {$custom_conffile} {
	    # Copy original conf file and remove all unwanted possible lines.
	    file copy -force $conf $conftmp
	    ::MembraneMixerPackage::WriteRemoveLines [list "mgridforce" "minimize" "numsteps" "run" "bincoordinates" "binvelocities" "extendedsystem" "outputname" "temperature" "cellbasisvector" "cellorigin" "margin" "langevinpistonperiod" "langevinpistondecay"] $conftmp $filetmp
	    if {$ChangeFreqOut} {
	        ::MembraneMixerPackage::WriteRemoveLines [list "dcdfreq" "outputenergies" "xstfreq" "restartfreq"] $conftmp $filetmp
	    }
	    if {$restrainedbilayer} {
	        ::MembraneMixerPackage::WriteRemoveLines "colvars" $conftmp $filetmp
	    }
	    if {$restrained} {
	        ::MembraneMixerPackage::WriteRemoveLines [list "constraint" "consexp" "consref" "conskfile" "conskcol" "selectConstr"] $conftmp $filetmp
	    }
	} else {
	    ::MembraneMixerPackage::WriteTMPConf $conftmp
	}
	puts2 "Done."
	puts2 ""

	# Centering membrane
	display update ui
	puts2 "Centering membrane in the simulation box..."
	if {!$script} {::MembraneMixerPackage::consoleMessage "Center membrane"}
	mol delete all
	mol new $psf
	mol addfile $pdb
	set IDi [molinfo top]
	display update ui
	set pbcvalue [pbc get]
	if {![lindex $pbcvalue 0 0]} {
    	puts2 "Warning: no pbc info in pdb file."
    	set mm [measure minmax [atomselect $IDi "water"]]
    	set pbcguess [vecsub [lindex $mm 1] [lindex $mm 0]]
    	if {[veclength $pbcguess]} {
        	pbc set $pbcguess
        	puts2 "The pbc will be set to minmax value of selection \"water\"."
    	} else {
        	set mm [measure minmax [atomselect $IDi "all"]]
        	set pbcguess [vecsub [lindex $mm 1] [lindex $mm 0]]
        	pbc set $pbcguess
        	puts2 "The pbc will be set to minmax value of selection \"all\"."
    	}
	}
	pbc box
	set all [atomselect $IDi "all"]
	# Wrap all atoms around the selection of reference. First center protein if present.
	if {[[atomselect $IDi "protein"] num]} {
    	    pbc wrap -sel "all" -center com -centersel "protein" -compound residue
    	    puts2 "Protein was centered."
	}
	pbc wrap -sel "all" -center com -centersel $MembSelection -compound residue
	puts2 "Membrane (selection \"$MembSelection\") was centered."
	set com_all [measure center $all weight mass]
	$all moveby [vecinvert [vecsub $com_all {0 0 0}]]
	$all delete
	# Save new pdb file
	set pdb "[file join $wd_master ${ROOTPSF}.CENTERED.pdb]"
	[atomselect $IDi all] writepdb $pdb
	puts2 "Centered membrane saved as: \"$pdb\". It will be used from now on in the script."
	# Save unit cell info to start simulations
	set xsc "[file join $wd_master ${ROOTPSF}.CENTERED.xsc]"
	pbc writexst $xsc -now
	puts2 "Unit cell info saved as:    \"$xsc\". It will be used from now on in the script."
	puts2 ""

	# Load initial structure
	mol delete all
	mol new $psf
	mol addfile $pdb
	set IDo [molinfo top]
	mol rename $IDo "Centered membrane"
	# Set some representations
	::MembraneMixerPackage::add_rep_all $IDo $MembSelection
	mol off $IDo
	display update ui

	# Get total amount of lipids in each leaflet
	set listmp [::MembraneMixerPackage::GetLeafLipids $IDo $MembSelection]
	set Leaf1 [lindex $listmp 0]
	set Leaf2 [lindex $listmp 1]
	set membZcenter [lindex $listmp 2]
	set NumLip1 [llength $Leaf1]
	set NumLip2 [llength $Leaf2]

	# Establish how many lipids will be exchanged. Must be an even number.
	# If NL1 and NL2 are zero then set them to 2, in order to exchange at least 2 lipids per leaflet.
	set NL1 [expr round($exchange*$NumLip1/100.)]
	if {![expr {($NL1 % 2) == 0}]} {set NL1 [expr $NL1 + 1]};# make it even if odd
	if {$NL1<2} {set NL1 2};# make it equal 2 if zero
	puts2 "Number $NumLip1 lipids in UPPER leaflet. Number of lipid to exchange in UPPER leaflet is $NL1."
	set NL2 [expr round($exchange*$NumLip2/100.)]
	if {![expr {($NL2 % 2) == 0}]} {set NL2 [expr $NL2 + 1]};# make it even if odd
	if {$NL2<2} {set NL2 2};# make it equal 2 if zero
	puts2 "Number $NumLip2 lipids in LOWER leaflet. Number of lipid to exchange in LOWER leaflet is $NL2."
	display update ui

###########################################################################################
	# Loop for generating multiple membranes
	for {set numrep 1} {$numrep<=$nreplicas} {incr numrep} {
	    # Format number of replica
	    if {$nreplicas < 10} {
	        set numrepF  [format %d $numrep]
	    } elseif {$nreplicas <= 99 && $nreplicas >= 10} {
	        set numrepF  [format %02d $numrep]
	    } else {
	        set numrepF  [format %03d $numrep]
	    }
	    puts2 ""
	    puts2 "********************************************************************************"
	    puts2 "                            REPLICA  NUMBER $numrepF "
	    puts2 "********************************************************************************"

	    # Reinitialize variable to skip replica to 0
	    set SKIPREP 0
	    # Create directory were to transfer simulation files after simulation
	    set name_dir MMP.${ROOTPSF}.REP${numrepF}
	    set wd $wd_master
	    set wdrep [file join $wd_master $name_dir]

	    # Get resid of lipids to exchange
	    puts3 -nonewline "Selecting lipids to exchange..."
	    if {!$script} {::MembraneMixerPackage::consoleMessage "Select lipid to exchange (R$numrepF)"}
	    set lisAllTMP1 $Leaf1
	    set lisAllTMP2 $Leaf2
	    set lisLeaf1 [::MembraneMixerPackage::get_res_leaflet $NL1 $lisAllTMP1]
	    set lisLeaf2 [::MembraneMixerPackage::get_res_leaflet $NL2 $lisAllTMP2]
	    puts2 "Done."
	    puts2 ""
	    puts2 "The following lipid resid in selection \"$MembSelection\" and UPPER leaflet: [lindex $lisLeaf1 0]"
	    puts2 "                                           will be exchanged with these: [lindex $lisLeaf1 1]"
	    puts2 ""
	    puts2 "The following lipid resid in selection \"$MembSelection\" and LOWER leaflet: [lindex $lisLeaf2 0]"
	    puts2 "                                           will be exchanged with these: [lindex $lisLeaf2 1]"
	    display update ui

	    # List of all lipids to exchange ordered by exchanging pairs
	    set lisEx {}
	    for {set i 0} {$i<[llength [lindex $lisLeaf1 0]]} {incr i} {
    	        lappend lisEx [lindex $lisLeaf1 0 $i]
    	        lappend lisEx [lindex $lisLeaf1 1 $i]
	    }
	    for {set i 0} {$i<[llength [lindex $lisLeaf2 0]]} {incr i} {
    	        lappend lisEx [lindex $lisLeaf2 0 $i]
    	        lappend lisEx [lindex $lisLeaf2 1 $i]
	    }
	    display update ui

	    # Load structure
	    if {$numrep>1} {
    	        mol off [lindex $lisMolID end]
    	        mol off [expr [lindex $lisMolID end] + 1] 
	    }
	    mol new $psf
	    mol addfile $pdb
	    set ID [molinfo top]
	    lappend lisMolID $ID
	    mol rename $ID "Lipid exchange REP$numrepF"
	    display update ui
	    # Set some representations
	    ::MembraneMixerPackage::add_rep_all $ID $MembSelection
	    # Add lipid representation
	    ::MembraneMixerPackage::add_rep_lipid $ID $MembSelection $lisEx
	    display update ui

###############################################################################################
	    #### Equilibrations (EQ1 or EQ2) ####
	    # BEGINNING of "foreach" loop
	    foreach PHASE {"EQ1" "EQ2"} tagPhase {"Equilibration 1" "Equilibration 2"} indPhase {1 2} {
	        if {!$script} {::MembraneMixerPackage::UpdateStatusText "$tagPhase - Replica $numrepF/$nreplicas"}
	        puts2 ""
	        puts2 $tagPhase
	        # Select molecule ID to work on
	        set ID [lindex $lisMolID end]

	        # Generate grid potential
	        if {!$script} {::MembraneMixerPackage::consoleMessage "Generate force grid (EQ${indPhase}-R$numrepF)"}
	        set sel [atomselect $ID "$MembSelection and resid $lisEx"]
	        set tag [file join $wd "MMP.FORCEGRID.REP${numrepF}.REF${indPhase}"]
	        puts3 -nonewline "Generating force grid..."
	        mdff sim $sel -o ${tag}.dx -res $resolution -spacing $spacing
	        mdff griddx -i ${tag}.dx -o ${tag}.dx
	        set IDgrid [molinfo top]
	        $sel delete
	        mol rename $IDgrid "FORCEGRID${indPhase}"
	        puts2 "Done."
	        mol modstyle 0 $IDgrid Isosurface 0.500000 0 0 2 1 1
	        if {$PHASE == "EQ1"} {
	        	mol modcolor 0 $IDgrid ColorID 3
	        } elseif {$PHASE == "EQ2"} {
	        	mol modcolor 0 $IDgrid ColorID 10
	            mol off [expr [lindex $lisMolID end-1] + 1] ;# turn off molID of first grid-force
	        }
	        display update ui

	        # Make grid reference file
	        puts3 -nonewline "Writing grid reference file..."
	        [atomselect $ID all] set occupancy 0
	        [atomselect $ID all] set beta 0
	        [atomselect $ID "not(protein or ($MembSelection and resid $lisEx))"] set occupancy 1
	        [atomselect $ID "not(protein or ($MembSelection and resid $lisEx))"] set beta -1
	        [atomselect $ID all] writepdb ${tag}.pdb
	        puts2 "Done."

	        # Make new input for simulation
	        set root [file join $wd MMP.${ROOTPSF}.REP${numrepF}.EQ${indPhase}]
	        set conf [::MembraneMixerPackage::MakeConfFile $ID $wd $psf $pdb $xsc $root $num_min($indPhase) $num_md($indPhase) $PHASE $indPhase $numrepF $restrainedbilayer $MembSelection $membZcenter $forceconstbilayer $restrained $restrGroupSelect $forceconst $conftmp $filetmp $ROOTPSF $ChangeFreqOut $parfiles]
	        display update ui

	        # Start simulation
	        if {!$script} {::MembraneMixerPackage::consoleMessage "Start NAMD simulation (EQ${indPhase}-R$numrepF)"}
	        set SKIPREP [::MembraneMixerPackage::StartFollowNAMD $root $conf $namdcommand $namdcommandOpt]
	        if {$SKIPREP} {set SKIPREP 1; break}

	        # Open trajectory 
	        puts3 -nonewline "Opening trajectory..."
	        if {!$script} {::MembraneMixerPackage::consoleMessage "Open trajectory (EQ${indPhase}-R$numrepF)"}
	        if {[file exists ${root}.dcd] == 0} {::MembraneMixerPackage::ErrorMessRun "Trajectory file does not exist! This might be due to an early crash of the simulation. SKIPPING THIS REPLICA!"; set SKIPREP 1; break}
	        mol off $ID
	        mol new $psf
	        mol addfile ${root}.dcd waitfor all
	        set IDlast [molinfo top]
	        lappend lisMolID $IDlast
	        mol rename $IDlast $tagPhase
	        # Set some representations
	        ::MembraneMixerPackage::add_rep_all $IDlast $MembSelection
	        # Add lipid representation
	        ::MembraneMixerPackage::add_rep_lipid $IDlast $MembSelection $lisEx
	        display update ui
	        puts2 "Done."

	        if {$PHASE == "EQ1"} {
	    	# Loop over all lipids to exchange
	    	puts2 "Exchanging lipids:"
	    	if {!$script} {::MembraneMixerPackage::consoleMessage "Exchange lipids (EQ${indPhase}-R$numrepF)"}
	    	set c 0
	    	for {set i 0} {$i<[expr [llength $lisEx]/2]} {incr i} { 
    	    	set r1 [lindex $lisEx $c]
    	    	set r2 [lindex $lisEx [expr $c + 1]]
    	    	set resname1 [lindex [[atomselect $IDlast "$MembSelection and resid $r1"] get resname] 0]
    	    	set resname2 [lindex [[atomselect $IDlast "$MembSelection and resid $r2"] get resname] 0]
    	    	if {![expr {(($i + 1) % 3) == 0}]} {
            		puts3 -nonewline "[format %4d $r1][format %7s "($resname1)"] <-->[format %4d $r2][format %7s "($resname2)"]   "
    	    	} else {
            		puts2 "[format %4d $r1][format %7s "($resname1)"] <-->[format %4d $r2][format %7s "($resname2)"]"
    	    	}
    	    	::MembraneMixerPackage::exchange_com $IDlast $MembSelection $r1 $r2 $ExProc
    	    	set c [expr $c + 2]
	    	}
	    	display update ui
	    	puts2 "Done."
	        }
	    } ;# END of "foreach" loop
	    # Continue to next replica if simulations crashed in EQ 1 or EQ2
	    if {$SKIPREP} {lappend lisSkipped $numrepF; ::MembraneMixerPackage::MoveSimulationFiles $wdrep $name_dir $numrepF; continue}

###############################################################################################
	    #### Ring Piercing checking and fixing ####
	    if {!$script} {::MembraneMixerPackage::UpdateStatusText "Checking ring piercing - Replica $numrepF/$nreplicas"}
	    if {!$script} {::MembraneMixerPackage::consoleMessage "Check ring piercing (R$numrepF)"}
	    mol off [expr [lindex $lisMolID end-1] + 1] ;# turn off molID of first grid-force
            puts2 ""
            # Check if there is ring piercing and, in that case, minimize structure until there is no more ring piercing
            set cutoff 2.50; # initial cutoff, it will be decreased in the loop
            set count 1
            set IDtest [lindex $lisMolID end]
            mol top $IDtest
            for {set P 0} {$P<=4} {incr P} {
                if {$count>=30} {puts2 "Too many ring piercing correction attempts. There might be a serious problem with this replica. SKIPPING THIS REPLICA."; set SKIPREP 1; break}
                puts2 "Checking ring piercing with cutoff $cutoff Ang"
                set IDlast $IDtest
                set piercing [::MembraneMixerPackage::CheckPiercing $IDlast $psf $cutoff]
                while {  [llength $piercing] } {
	            if {!$script} {::MembraneMixerPackage::UpdateStatusText "Fixing ring piercing - Replica $numrepF/$nreplicas"}
                    set IDlast $IDtest
                    set tag [expr $count + 2]
                    set allpiercing {}
                    puts2 "WARNING, RING PIERCING!"
                    puts2 "Trying to correct for piercing: ATTEMPT $count"
                    puts2 "Move piercing atoms..."
                    # Move atoms piercing rings and minimize
                    for {set i 0} {$i<[llength $piercing]} {incr i} {
                        set indeces [lindex $piercing $i]
                        ::MembraneMixerPackage::MovePiercing [lindex $indeces 0] [lindex $indeces 1] $IDlast $count
                        lappend allpiercing [lindex $indeces 0]
                        lappend allpiercing [lindex $indeces 1]
                    }
                    ::MembraneMixerPackage::add_rep_piercing $allpiercing $IDlast
                    display update ui
                    
                    # Make new input for minimization
                    puts2 "Equilibration ${tag} (ring piercing)"
                    set root [file join $wd MMP.${ROOTPSF}.REP${numrepF}.EQ${tag}]
                    set conf [::MembraneMixerPackage::MakeConfFile $IDlast $wd $psf $pdb $xsc $root $num_min(1) $num_md(1) "RP" $tag $numrepF $restrainedbilayer $MembSelection $membZcenter $forceconstbilayer $restrained $restrGroupSelect $forceconst $conftmp $filetmp $ROOTPSF $ChangeFreqOut $parfiles]
                    
                    # Start minimization
                    if {!$script} {::MembraneMixerPackage::consoleMessage "Fix ring piercing (EQ${tag}-R$numrepF)"}
                    set SKIPREP [::MembraneMixerPackage::StartFollowNAMD $root $conf $namdcommand $namdcommandOpt]
	            if {$SKIPREP} {break}
                    
                    # Open trajectory
                    puts3 -nonewline "Opening trajectory..."
	    	    if {!$script} {::MembraneMixerPackage::consoleMessage "Open trajectory (EQ${tag}-R$numrepF)"}
                    if {[file exists ${root}.dcd] == 0} {::MembraneMixerPackage::ErrorMessRun "Trajectory file does not exist! This might be due to an early crash of the simulation. SKIPPING THIS REPLICA!"; set SKIPREP 1; break}
                    mol off $IDlast
                    mol new $psf
                    mol addfile ${root}.dcd waitfor all
                    set IDtest [molinfo top]
                    mol rename $IDtest "Ring-piercing attempt $count"
                    display update ui
                    puts2 "Done."
                    
                    # Set some representations
                    ::MembraneMixerPackage::add_rep_all $IDtest $MembSelection
                    # Add lipid representation
                    ::MembraneMixerPackage::add_rep_lipid $IDtest $MembSelection $lisEx
                    display update ui
                    # Check again ring piercing
                    set piercing [::MembraneMixerPackage::CheckPiercing $IDtest $psf $cutoff]
                    incr count
                }
	        if {$SKIPREP} {break}
                set cutoff [format %.2f [expr $cutoff - 0.15]]
            }
	    if {$SKIPREP} {lappend lisSkipped $numrepF; ::MembraneMixerPackage::MoveSimulationFiles $wdrep $name_dir $numrepF; continue}
            display update ui
            if {$count>1} {puts2 "Ring piercings were detected and corrected. However, check always the structure for additional issues."}
            # Check chirality and cispeptide bonds in protein if present
            if {[[atomselect $IDlast "protein"] num]} {
                display update ui
                puts2 "Checking chirality errors in protein..."
                set checkchir [chirality check -mol $IDlast -seltext "protein"]
                if {$checkchir} {
                    puts2 "Found chirality error! Please check and correct for the errors in the protein if needed."
	    	    if {!$script} {::MembraneMixerPackage::consoleMessage "FOUND CHIRALITY ERROR (R$numrepF)"}
                } else {
                    puts2 "* No chirality errors *"
                }
                display update ui
                puts2 "Checking cispeptide errors in protein..."
                set checkcis [cispeptide check -mol $IDlast -seltext "protein"]
                if {$checkcis} {
                    puts2 "Found cispeptide error! Please check and correct for the errors in the protein if needed."
                    puts2 "Remember that some cispeptide bonds are naturally occurring, like in proline."
	    	if {!$script} {::MembraneMixerPackage::consoleMessage "FOUND CISPEPTIDE ERROR (R$numrepF)"}
                } else {
                    puts2 "* No cispeptide errors *"
                }
                display update ui
            }
            
            # Save pdb file containing final membrane replica
            set outpdb [file join $wd_master ${ROOTPSF}.REPLICA${numrepF}.pdb]
            set outxsc [file join $wd_master ${ROOTPSF}.REPLICA${numrepF}.xsc]
            [atomselect $IDlast all] writepdb $outpdb
            pbc writexst $outxsc -now
            puts2 ""
            puts2 " *** Final membrane replica saved as: $outpdb"
            puts2 " *** Unit cell information  saved as: $outxsc"
	    if {!$script} {::MembraneMixerPackage::consoleMessage "Replica generated and saved (R$numrepF)"}
	    lappend lisGenerated $numrepF
	    ::MembraneMixerPackage::MoveSimulationFiles $wdrep $name_dir $numrepF
            
        } ;# End of NumReplica loop
	# Remove unuseful files
	file delete "MMP.FILETMP.conf"
	file delete "MMP.CONFTMP.conf"
	# Summary of run
        puts2 ""
        puts2 "*************************************"
        puts2 ""
	puts2 "GENERATED MEMBRANES: $lisGenerated (total [llength $lisGenerated])"
	puts2 "SKIPPED MEMBRANES: $lisSkipped (total [llength $lisSkipped])"
        puts2 ""
        puts2 "*************************************"
        puts2 "*** It works like a charmM-GUI ;-) **"
        puts2 "*************************************"
        puts2 ""
	if {!$script} {
	    ::MembraneMixerPackage::consoleMessage "*** It works like a charmM-GUI ;-) ***"
	    ::MembraneMixerPackage::UpdateStatusText "IDLE"
	    ::MembraneMixerPackage::UpdateStateRun normal
   	}
###########################################################################################

}

# vmd_install_extension membranemixerpackage membranemixerpackage_tk "Modeling/Membrane Mixer Package"
